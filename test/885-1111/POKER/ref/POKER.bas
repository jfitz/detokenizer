10 REM *** DRAW POKER ***
20 REM
30 REM BY EARL D. WILSON
40 REM    317 RIVERVIEW DRIVE
50 REM    AUBURN, CA 95603
60 REM
70 REM MODIFIED BY P. SWAYNE, HUG
80 REM
90 E$=CHR$(27):F$=E$+"F":G$=E$+"G":P$=E$+"p":Q$=E$+"q":Y$=E$+"Y"
100 DIM D(56),V(5),N$(12),R$(5),S$(3),V$(5),W$(5)
110 S=RND(-PEEK(8219)):S=0
120 DEF FN M(M1,M2)=M1-(M2*INT(M1/M2))
130 FOR I=0 TO 12:READ N$(I):NEXT I
140 DATA "A","2","3","4","5","6","7","8","9","10","J","Q","K"
150 FOR I=0 TO 3:READ S$(I):NEXT I
160 DATA "D","C","H","S"
170 PRINT E$"E";E$"x5";E$"x1";Y$"8 ";P$
180 PRINT "Type 'f' keys for cards to be held, BLUE when ready to draw, RED to cancel"
190 PRINT Y$" B";"DRAW POKER";Q$
200 PRINT "   ROYAL FLUSH    250";TAB(63);"FLUSH           7"
210 PRINT "   STRAIGHT FLUSH 100";TAB(63);"STRAIGHT        5"
220 PRINT "   FOUR OF A KIND  40";TAB(63);"THREE OF A KIND 3"
230 PRINT "   FULL HOUSE      10";TAB(63);"TWO PAIR        2"
240 PRINT Y$"#B";"$1 per play"
250 PRINT Y$"'(";TAB(4);"1";TAB(19);"2";TAB(34);"3";TAB(49);"4";TAB(64);"5"
260 PRINT F$;P$;
270 PRINT "   {{{{{{{{{{{    {{{{{{{{{{{    {{{{{{{{{{{    {{{{{{{{{{{    {{{{{{{{{{{   "
280 FOR I=1 TO 11
290 PRINT "   |         }    |         }    |         }    |         }    |         }   "
300 NEXT I
310 PRINT "   zzzzzzzzzzz    zzzzzzzzzzz    zzzzzzzzzzz    zzzzzzzzzzz    zzzzzzzzzzz   "
320 FOR A=1 TO 5:B=21+15*A:GOSUB 690:GOSUB 740:NEXT A
330 RESTORE 
340 PRINT Y$"6 ";G$;Q$;"Press SPACE bar to deal cards     ":A$=INPUT$(1)
350 IF A$<>" " GOTO 340
360 PRINT Y$"6 ";TAB(40);" "
370 C1=52:FOR I=0 TO 51:D(I)=I:NEXT I
380 FOR A=1 TO 5:B=21+15*A
390 GOSUB 690:GOSUB 860:GOSUB 900
400 V$(A)=N$(FN M(C,13)):W$(A)=S$(FN M(C,4)):GOSUB 970
410 NEXT A
420 PRINT Q$;:PRINT Y$"6 See below.  Press the RED key to correct mistakes, WHITE to quit."
430 A$=INPUT$(1):IF A$=CHR$(27) THEN A$=INPUT$(1)
440 IF A$="S"THEN R$(1)="H":A=1:GOSUB 1280:GOTO 530
450 IF A$="T"THEN R$(2)="H":A=2:GOSUB 1280:GOTO 530
460 IF A$="U"THEN R$(3)="H":A=3:GOSUB 1280:GOTO 530
470 IF A$="V"THEN R$(4)="H":A=4:GOSUB 1280:GOTO 530
480 IF A$="W"THEN R$(5)="H":A=5:GOSUB 1280:GOTO 530
490 IF A$="P"THEN GOTO 540
500 IF A$="Q" THEN GOSUB 1290
510 IF A$="Q"THEN GOSUB 1290
520 IF A$="R"THEN PRINT E$"z":FOR I=1 TO 10:NEXT:PRINT"Thanks for playing.":SYSTEM
530 GOTO 420
540 FOR A=1 TO 5
550 IF R$(A)<>"H"THEN B=21+15*A:GOSUB 690:GOSUB 740
560 NEXT A
570 FOR A=1 TO 5
580 IF R$(A)<>"H"THEN B=21+15*A:GOSUB 690:GOSUB 860:GOSUB 900
590 IF R$(A)<>"H"THEN V$(A)=N$(FN M(C,13)):W$(A)=S$(FN M(C,4)):GOSUB 970
600 NEXT A
610 PRINT Q$;:PRINT Y$"6 ";TAB(75);" ":GOTO 1320
620 PRINT Y$"6 ";Q$;"You won $";W;"                       ":S=S+W-1:GOTO 640
630 PRINT Y$"6 ";Q$;"You lost. Better luck next time.":S=S-W
640 PRINT Y$"%<";"You are now";
650 IF S=0THEN PRINT " even.           ":X9=6:GOSUB 2100:GOTO 680
660 IF S<0THEN PRINT " $";S;" (behind)":X9=6:GOSUB 2100:GOTO 680
670 PRINT " $";S;"ahead     "
680 GOSUB 1290:GOTO 320
690 PRINT F$;P$;:FOR J=1 TO 11
700 PRINT Y$CHR$(40+J)CHR$(B);"         "
710 NEXT J
720 PRINT G$;Q$:RETURN 
730 PRINT 
740 PRINT F$;P$
750 PRINT Y$"*"CHR$(B);"y y   x x"
760 PRINT Y$"+"CHR$(B);" y y x x"
770 PRINT Y$","CHR$(B);"y y   x x"
780 PRINT Y$"-"CHR$(B);" y y x x"
790 PRINT Y$"."CHR$(B);"    w"
800 PRINT Y$"/"CHR$(B);" x x y y"
810 PRINT Y$"0"CHR$(B);"x x   y y"
820 PRINT Y$"1"CHR$(B);" x x y y"
830 PRINT Y$"2"CHR$(B);"x x   y y"
840 PRINT G$;Q$:RETURN 
850 PRINT 
860 X=INT(C1*RND(1)):C=D(X)
870 FOR I=X TO 50:D(I)=D(I+1):NEXT I
880 D(51)=C:C1=C1-1:RETURN 
890 PRINT 
900 PRINT P$:PRINT Y$")"CHR$(B);N$(FN M(C,13))
910 PRINT Y$"*"CHR$(B);S$(FN M(C,4))
920 PRINT Y$"2"CHR$(B+8);S$(FN M(C,4))
930 IF VAL(N$(FN M(C,13)))=10 THEN B=B-1
940 PRINT Y$"3"CHR$(B+8);N$(FN M(C,13))
950 RETURN 
960 PRINT 
970 V=VAL(V$(A))
980 IF V$(A)="A" THEN V=1:GOTO 1020
990 IF V$(A)="J" THEN V=11:GOTO 1020
1000 IF V$(A)="Q" THEN V=12:GOTO 1020
1010 IF V$(A)="K" THEN V=13
1020 ON V GOTO 1030,1040,1050,1060,1070,1080,1090,1100,1110,1120,1130,1140,1150
1030 PRINT Y$"."CHR$(B+3);"ACE":RETURN 
1040 PRINT Y$"+"CHR$(B+4);W$(A):PRINT Y$"1"CHR$(B+4);W$(A):RETURN 
1050 PRINT Y$"+"CHR$(B+4);W$(A):PRINT Y$"."CHR$(B+4);W$(A):GOTO 1160
1060 PRINT Y$"+"CHR$(B+2);W$(A);"   ";W$(A):GOTO 1170
1070 PRINT Y$"+"CHR$(B+2);W$(A);"   ";W$(A):PRINT Y$"."CHR$(B+4);W$(A):GOTO 1180
1080 PRINT Y$"+"CHR$(B+2);W$(A);"   ";W$(A):PRINT Y$"."CHR$(B+2);W$(A);"   ";W$(A):GOTO 1190
1090 PRINT Y$"+"CHR$(B+2);W$(A);"   ";W$(A):PRINT Y$","CHR$(B+4);W$(A):GOTO 1200
1100 PRINT Y$"+"CHR$(B+2);W$(A);"   ";W$(A):PRINT Y$","CHR$(B+4);W$(A):GOTO 1210
1110 PRINT Y$"+"CHR$(B+2);W$(A);"   ";W$(A):PRINT Y$"-"CHR$(B+2);W$(A);"   ";W$(A):GOTO 1230
1120 PRINT Y$"+"CHR$(B+3);W$(A);"   ";W$(A):PRINT Y$","CHR$(B+5);W$(A):GOTO 1250
1130 PRINT Y$"."CHR$(B+2);"JACK":RETURN 
1140 PRINT Y$"."CHR$(B+2);"QUEEN":RETURN 
1150 PRINT Y$"."CHR$(B+2);"KING":RETURN 
1160 PRINT Y$"1"CHR$(B+4);W$(A):RETURN 
1170 PRINT Y$"1"CHR$(B+2);W$(A);"   ";W$(A):RETURN 
1180 PRINT Y$"1"CHR$(B+2);W$(A);"   ";W$(A):RETURN 
1190 PRINT Y$"1"CHR$(B+2);W$(A);"   ";W$(A):RETURN 
1200 PRINT Y$"."CHR$(B+2);W$(A);"   ";W$(A):PRINT Y$"1"CHR$(B+2);W$(A);"   ";W$(A):RETURN 
1210 PRINT Y$"."CHR$(B+2);W$(A);"   ";W$(A):PRINT Y$"0"CHR$(B+4);W$(A)
1220 PRINT Y$"1"CHR$(B+2);W$(A);"   ";W$(A):RETURN 
1230 PRINT Y$"."CHR$(B+4);W$(A)
1240 PRINT Y$"/"CHR$(B+2);W$(A);"   ";W$(A):PRINT Y$"1"CHR$(B+2);W$(A);"   ";W$(A):RETURN 
1250 PRINT Y$"-"CHR$(B+3);W$(A);"   ";W$(A):PRINT Y$"/"CHR$(B+3);W$(A);"   ";W$(A)
1260 PRINT Y$"0"CHR$(B+5);W$(A):PRINT Y$"1"CHR$(B+3);W$(A);"   ";W$(A):RETURN 
1270 PRINT 
1280 PRINT Y$"'"CHR$(23+15*A);"HOLD":RETURN 
1290 PRINT Y$"'&";"    ":PRINT Y$"'(";TAB(4);"1 ";TAB(19);"2 ";TAB(34);"3 ";
1300 PRINT TAB(49);"4 ";TAB(64);"5 ":R$(1)="":R$(2)="":R$(3)="":R$(4)="":R$(5)=""
1310 RETURN 
1320 FOR J=2 TO 5:IF V$(1)=V$(J)THEN 1470
1330 NEXT J
1340 FOR J=3 TO 5:IF V$(2)=V$(J)THEN 1470
1350 NEXT J
1360 IF V$(3)=V$(4)THEN 1470
1370 IF W$(1)=W$(2)THEN IF W$(1)=W$(3)THEN IF W$(1)=W$(4)THEN 1390
1380 GOTO 1400
1390 IF W$(1)=W$(5)THEN 1430
1400 GOSUB 1880
1410 IF Z=1THEN W=5:GOTO 620
1420 W=1:GOTO 630
1430 GOSUB 1880
1440 IF Z=0THEN W=7:GOTO 620
1450 IF V(5)=14THEN W=250:GOTO 620
1460 W=100:GOTO 620
1470 IF V$(1)=V$(2)THEN IF V$(2)=V$(3)THEN X$=V$(1):GOTO 1710
1480 IF V$(1)=V$(2)THEN IF V$(2)=V$(4)THEN X$=V$(1):GOTO 1710
1490 IF V$(1)=V$(2)THEN IF V$(2)=V$(5)THEN X$=V$(1):GOTO 1710
1500 IF V$(1)=V$(3)THEN IF V$(3)=V$(4)THEN X$=V$(1):GOTO 1710
1510 IF V$(1)=V$(3)THEN IF V$(3)=V$(5)THEN X$=V$(1):GOTO 1710
1520 IF V$(1)=V$(4)THEN IF V$(4)=V$(5)THEN X$=V$(1):GOTO 1710
1530 IF V$(2)=V$(3)THEN IF V$(3)=V$(4)THEN X$=V$(2):GOTO 1710
1540 IF V$(2)=V$(3)THEN IF V$(3)=V$(5)THEN X$=V$(2):GOTO 1710
1550 IF V$(2)=V$(4)THEN IF V$(4)=V$(5)THEN X$=V$(2):GOTO 1710
1560 IF V$(3)=V$(4)THEN IF V$(4)=V$(5)THEN X$=V$(3):GOTO 1710
1570 FOR J=2 TO 5:IF V$(1)=V$(J)THEN V$(1)="0":V$(J)="0":GOTO 1620
1580 NEXT J
1590 FOR J=3 TO 5:IF V$(2)=V$(J)THEN V$(2)="0":V$(J)="0":GOTO 1620
1600 NEXT J
1610 W=1:GOTO 630
1620 FOR J=2 TO 5:IF V$(1)=V$(J)THEN IF V$(1)<>"0"THEN 1700
1630 NEXT J
1640 FOR J=3 TO 5:IF V$(2)=V$(J)THEN IF V$(2)<>"0"THEN 1700
1650 NEXT J
1660 FOR J=4 TO 5:IF V$(3)=V$(J)THEN IF V$(3)<>"0"THEN 1700
1670 NEXT J
1680 IF V$(4)=V$(5)THEN IF V$(4)<>"0"THEN 1700
1690 W=1:GOTO 630
1700 W=2:GOTO 620
1710 IF V$(1)=V$(2)THEN IF V$(2)=V$(3)THEN IF V$(3)=V$(4)THEN 1770
1720 IF V$(1)=V$(2)THEN IF V$(2)=V$(3)THEN IF V$(3)=V$(5)THEN 1770
1730 IF V$(1)=V$(2)THEN IF V$(2)=V$(4)THEN IF V$(4)=V$(5)THEN 1770
1740 IF V$(1)=V$(3)THEN IF V$(3)=V$(4)THEN IF V$(4)=V$(5)THEN 1770
1750 IF V$(2)=V$(3)THEN IF V$(3)=V$(4)THEN IF V$(4)=V$(5)THEN 1770
1760 GOTO 1780
1770 W=40:GOTO 620
1780 FOR J=2 TO 5:IF V$(1)=V$(J)THEN IF V$(1)<>X$THEN 1860
1790 NEXT J
1800 FOR J=3 TO 5:IF V$(2)=V$(J)THEN IF V$(2)<>X$THEN 1860
1810 NEXT J
1820 FOR J=4 TO 5:IF V$(3)=V$(J)THEN IF V$(3)<>X$THEN 1860
1830 NEXT J
1840 IF V$(4)=V$(5)THEN IF V$(1)=V$(2)THEN IF V$(2)=V$(3)THEN 1860
1850 W=3:GOTO 620
1860 W=10:GOTO 620
1870 PRINT 
1880 FOR J=1 TO 5
1890 V(J)=VAL(V$(J))
1900 IF V$(J)="A"THEN V(J)=1
1910 IF V$(J)="J"THEN V(J)=11
1920 IF V$(J)="Q"THEN V(J)=12
1930 IF V$(J)="K"THEN V(J)=13
1940 NEXT J
1950 FOR J=1 TO 5:IF V(J)=13 THEN 1980
1960 NEXT J
1970 GOTO 2000
1980 FOR J=1 TO 5:IF V(J)=1 THEN V(J)=14
1990 NEXT J
2000 FOR K=1 TO 4
2010 FOR J=K+1 TO 5
2020 IF V(K)<=V(J)THEN 2040
2030 T=V(K):V(K)=V(J):V(J)=T
2040 NEXT J
2050 NEXT K
2060 IF V(5)=V(4)+1THEN IF V(4)=V(3)+1THEN IF V(3)=V(2)+1THEN 2080
2070 GOTO 2090
2080 IF V(2)=V(1)+1THEN Z=1:RETURN 
2090 Z=0:RETURN 
2100 POKE 8219,0:POKE 8220,0
2110 IF PEEK(8220)=X9 THEN RETURN :ELSE 2110
