10 REM *** "SECTOR" IS AN AEROSPACE WAR SIMULATION GAME BY: ***
20 REM ***                 C. F. MOWERY, JR.                ***
30 REM ***                 13305 S. 26TH AVE.               ***
40 REM ***                 OMAHA, NE 68123                  ***
50 REM ********************************************************
60 REM  MODIFIED BY P. SWAYNE, HUG
70 REM 
80 REM *** SET NORMAL TERMINAL MODES, THEN 5000 FOR GAME INTRODUCTION ***
90 CLEAR 500 
100 DEFINT A-Z:GOSUB4430:H$=CHR$(27):PRINT H$"q";H$"G";H$"y5";H$"y1":GOTO 2430
110 REM 
120 REM *** TO SELECT A POINT IN A SECTOR TO PRINT ALIEN/ALLIED CHARACTER ***
130 REM 
140 PRINT Y$:Z=INT(6*RND(1)):IF Z=0THEN GOSUB 910:GOTO 2340
150 IF I(Z)=1OR O(Z)=1GOTO 140:REM *** SECTOR INOPERATIVE/CONTROLLED ***
160 ON Z GOTO 170,180,190,200,210
170 A=40:B=59:GOTO 220
180 A=32:B=35:GOTO 220
190 A=32:B=84:GOTO 220
200 A=47:B=35:GOTO 220
210 A=47:B=84
220 Y=INT(7*RND(1)+1):X=INT(18*RND(1)+1):IF Z=1AND Y=7GOTO 220
230 IF S(Z,Y)<-12OR S(Z,Y)>12GOTO 220:REM *** 26 IF LINE Y CONTROLLED ***
240 A1$=E$+"Y"+CHR$(A+Y)+CHR$(B+X)
250 V=INT(5*RND(1)+1):ON V GOTO 260,270,280,300,310
260 C$="^":GOTO 290
270 C$="~":GOTO 290
280 C$="g"
290 GOSUB 2110:GOTO 330
300 C$="j":GOTO 320
310 C$=">"
320 GOSUB 2240
330 PRINT A1$;C$;:REM *** PRINT ALIEN/ALLIED CHARACTER(S) AT SITE SELECTED ***
340 IF V>3THEN X9=1:GOSUB4490
350 IF V<4THEN PRINT I$(4)"\aALIEN: SECTOR";Z;"LINE";Y;:X9=1:GOSUB4490:GOSUB 2620
360 IF V>3AND H(Z,Y)>0THEN FOR A1=1TO LEN(C$):PRINT CHR$(8);:NEXT 
370 IF V>3AND H(Z,Y)>0THEN FOR A1=1TO LEN(C$):PRINT "b";:NEXT :GOTO 390
380 PRINT P$;A1$;:FOR R1=1TO LEN(C$):PRINT " ";:NEXT :PRINT Q$;L$(1)
390 PRINT L$(1)
400 REM 
410 REM *** RANDOM CHANCE (1 IN 101) THAT SECTOR WILL MALFUNCTION ***
420 REM 
430 W=INT(100*RND(1)):IF W=0THEN S=INT(5*RND(1)+1):IF I(S)=1OR O(S)=1GOTO 670
440 IF W=0THEN M=1:S$="w":Z1=Z:Y1=Y:GOSUB 1220:Z=Z1:Y=Y1
450 REM 
460 REM *** TO DETERMINE EFFECT OF ALIEN/ALLIED CHARACTER ON LINE/SECTOR ***
470 REM 
480 IF H(Z,Y)>0AND V<4THEN H(Z,Y)=H(Z,Y)-LEN(C$)
490 IF H(Z,Y)<0THEN S(Z,Y)=S(Z,Y)-ABS(H(Z,Y)):H(Z,Y)=0:GOTO 530
500 IF H(Z,Y)>0GOTO 530
510 IF V>3AND S(Z,Y)<13AND S(Z,Y)>-13THEN S(Z,Y)=S(Z,Y)+LEN(C$)
520 IF V<4AND S(Z,Y)<13AND S(Z,Y)>-13THEN S(Z,Y)=S(Z,Y)-LEN(C$)
530 IF S(Z,Y)>12THEN C$=" F ":PRINT A1$;C$;I$(4)"SECTOR";Z;"LINE";Y;":"
540 IF S(Z,Y)>12THEN PRINT I$(5)"ALLIED CONTROL";
550 IF S(Z,Y)<-12THEN PRINT I$(4)"SECTOR";Z;"LINE";Y;":";I$(5)"ALIEN CONTROL";
560 IF S(Z,Y)<-12OR S(Z,Y)>12THEN X9=2:GOSUB4490:GOSUB 2620
570 IF S(Z,Y)<-12THEN C$=" A ":PRINT A1$;C$;:X9=2:GOSUB4490
580 X=0:X1=0:X2=0:FOR Y=1TO 7:IF S(Z,Y)<-12THEN X=X+1
590 IF S(Z,Y)>12THEN X1=X1+1
600 NEXT :IF X>3THEN M=2:S$="A":PRINT I$(2)"ALIENS CONTROL SECTOR";Z
610 IF X1>3THEN I(Z)=0:M=0:S$="F":PRINT I$(2)"ALLIES \aCONTROL SECTOR";Z:O(Z)=1
620 IF X>3OR X1>3THEN S=Z:GOSUB 1220:GOSUB 2620
630 X=0:FOR Z=1TO 5:X=X+I(Z):X2=X2+O(Z):NEXT 
640 IF X=5THEN PRINT I$(1)"ALL SECTORS INOPERATIVE\a!";I$(2)"YOU LOSE!":GOTO 660
650 IF X2=5THEN PRINT I$(1)"FULL ALLIED \aCONTROL!";I$(2)"YOU \aWIN!!!"
660 IF X+X2=5THEN PRINT I$(4)"SCORE:";I$(6)"ALIENS";X;I$(7)"ALLIES";X2:STOP 
670 RETURN 
680 REM 
690 REM *** TO SHOW SCORE WHENEVER "SCAN" IS SELECTED (LINE 2060) ***
700 REM 
710 F=0:F1=0:PRINT X$;L$(25);K$;SPC(10);
720 FOR A1=1TO 5:FOR B1=1TO 7:IF S(A1,B1)>12THEN F=F+1
730 IF S(A1,B1)<-12THEN F1=F1+1
740 NEXT :NEXT 
750 PRINT "ALLIES CONTROL";F;"LINES";SPC(5);"ALIENS CONTROL";F1;"LINES";
760 X9=2:GOSUB4490:PRINT :PRINT L$(25);SPC(5);"ALLIES CONTROL";X1;"SECTORS";SPC(5);
770 PRINT "ALIENS CONTROL";X;"SECTORS     ";:X9=2:GOSUB4490:PRINT Y$;L$(1)
780 RETURN 
790 REM 
800 REM *** TO FLASH 25TH LINE FUNCTION KEY TITLES/INDICATORS ***
810 REM 
820 PRINT CHR$(7)
830 PRINT J$;X$;F$:PRINT L$(25);SPC(6);"k S E C T O R k C O N T R O L S k"
840 IF Q>0THEN Q=Q-1:IF Q>1THEN PRINT L$(25);SPC(39):GOTO 830
850 IF Q>0THEN Q=0:PRINT J1$:RETURN 
860 PRINT CHR$(7)
870 PRINT E$;"Y8N`SHIELD`kFIREk`SCAN`"
880 IF Q1>0THEN Q1=Q1-1:IF Q1>1THEN PRINT E$;"Y8N";K$:FOR II=1 TO 10:NEXT:GOTO 870
890 IF Q1>0THEN Q1=0:PRINT J1$:RETURN 
900 GOTO 3850
910 REM 
920 REM *** OPERATION OF 8 FUNCTIONS KEYS ***
930 REM 
940 D=0:Q=5:Q1=0:GOSUB 790
950 DEF USR0=VARPTR(U0(0)):F0=USR0(0):IF F0=27 THEN A$=INPUT$(1):GOTO 980
960 GOSUB4440:D=D+1:IF D=P THEN RETURN 
970 GOTO 950
980 GOSUB4440
990 IF ASC(A$)<80OR ASC(A$)>87THEN GOSUB 2620:PRINT I$(1)"NO ENTRY\a!"
1000 IF ASC(A$)<80OR ASC(A$)>87THEN X9=2:GOSUB4490:GOSUB 2620:RETURN 
1010 ON ASC(A$)-79GOTO 1020,1030,1040,1050,1060,1070,1080,1090
1020 S$="b":GOTO 1660:REM *** SHIELD ***
1030 S$="h":GOTO 1920:REM *** FIRE ***
1040 S$="`":GOTO 2060:REM *** SCAN ***
1050 S=1:GOTO 1100:REM *** SECTOR # ***
1060 S=2:GOTO 1100:REM        "   "
1070 S=3:GOTO 1100:REM        "   "
1080 S=4:GOTO 1100:REM        "   "
1090 S=5:REM                  "   "
1100 IF A=1THEN RETURN 
1110 PRINT I$(4)"SECTOR";S;"SELECTED";:X9=2:GOSUB4490:GOSUB 2620
1120 Q=0:Q1=5:GOSUB 790:D=0:GOTO 950
1130 REM 
1140 REM *** TO AVOID USE OF RETURN KEY ***
1150 REM 
1160 A$=INPUT$(1):RETURN
1170 B$=INPUT$(1)
1180 IF ASC(B$)<48OR ASC(B$)>57THEN B=-1:RETURN 
1190 B=VAL(B$):RETURN 
1200 A$=INPUT$(1):A$=INPUT$(1):REM * INPUT A FUNCTION KEY
1210 GOTO 990
1220 REM 
1230 REM *** TO DISPLAY STATUS OF A SECTOR ***
1240 REM 
1250 X=0:L=406:D=0:IF I(S)=1GOTO 1640
1260 IF M<>1THEN PRINT P$:GOTO 1310
1270 PRINT I$(4)"SECTOR";S;"MALFUNCTIONING\a!";P$:IF D=P GOTO 1310
1280 DEF USR0=VARPTR(U0(0)):F0=USR0(0):GOSUB4450:IF CHR$(F0)="F"THEN M=0:S$=" "
1290 IF M=0THEN PRINT I$(4);"\aMALFUNCTION REPAIRED!":X9=2:GOSUB4490:GOSUB 2620:GOTO L
1300 D=D+1:GOTO 1270
1310 IF X>0AND X/2=INT(X/2)THEN PRINT Q$
1320 IF S$<>" "AND X=0THEN PRINT Q$
1330 FOR Z=1TO 7:ON S GOTO 1340,1360,1370,1380,1390
1340 FOR Z=1TO 6
1350 PRINT S1$(Z);S$;:GOTO 1400
1360 PRINT S2$(Z);:GOTO 1400
1370 PRINT S3$(Z);:GOTO 1400
1380 PRINT S4$(Z);:GOTO 1400
1390 PRINT S5$(Z);
1400 IF A=1THEN PRINT SPC(23);L$(1):GOTO 1570
1410 H=1:REM *** SEE LINE 442 - AVOIDS READING LINES 1460-1520 A SECOND TIME ***
1420 FOR Y=1TO 22:PRINT S$;:IF M=1GOTO 1530
1430 IF S(S,Z)>12THEN PRINT " F ":Y=23:GOTO 1530
1440 IF S(S,Z)<-12THEN PRINT " A ":Y=23:GOTO 1530
1450 IF H=0GOTO 1530
1460 IF S$="h"THEN GOSUB 1970:GOTO 1530:REM *** EFFECT OF FIRING ***
1470 IF S(S,Z)>0THEN FOR A1=1TO S(S,Z):PRINT ">";
1480 IF S(S,Z)>0AND H(S,Z)>0THEN PRINT CHR$(8);"b";
1490 IF S(S,Z)>0THEN NEXT :Y=Y+S(S,Z):GOTO 1530
1500 IF S(S,Z)<0THEN FOR A1=1TO ABS(S(S,Z)):PRINT "~";
1510 IF S(S,Z)<0AND H(S,Z)>0THEN PRINT CHR$(8);P$;" ";Q$;
1520 IF S(S,Z)<0THEN NEXT :Y=Y+ABS(S(S,Z))
1530 H=0:NEXT :PRINT L$(1):IF M=0AND H(S,Z)>0THEN GOSUB 1900
1540 IF M<>1GOTO 1570
1550 IF X<3THEN X=X+1
1560 IF X>0AND X<3GOTO 1270
1570 NEXT :A=0:IF S$="b"GOTO 1620:REM *** LEAVE SHIELD INTACT ***
1580 IF S$<>" "THEN IF M=0THEN PRINT P$:A=1:S$=" ":GOTO 1330
1590 IF S$<>" "THEN S$=" ":IF M<>0THEN X=1
1600 IF X>0THEN X=0:PRINT Q$:A=1:GOTO 1330
1610 IF M=2OR O(S)=1THEN GOSUB 2620:PRINT I$(4)"SECTOR";S;"CONTROLLE\aD"
1620 IF M=2OR O(S)=1THEN I(S)=1:GOTO 1650
1630 IF M=0THEN GOSUB 2620:PRINT I$(4);" SECTOR";S;"OPERATIVE":GOTO 1650
1640 GOSUB 2620:PRINT I$(4)" SECTOR";S;"\aINOPERATIVE!":I(S)=1
1650 X9=2:GOSUB4490:GOSUB 2620:M=0:RETURN 
1660 REM 
1670 REM *** TO ACTIVATE SHIELD (MOST LINES USED FOR FIRE AND SCAN ALSO) ***
1680 REM 
1690 X=0:PRINT I$(1)"SHIELD SELECTED";
1700 PRINT I$(3)"FOR SECTOR";S:X9=2:GOSUB4490
1710 IF I(S)=1THEN PRINT I$(3)"SECTOR INOPERATIV\aE":X9=2:GOSUB4490:GOSUB 2620:RETURN 
1720 GOSUB 2620:PRINT I$(3);C1$;"ENTER A LINE # OR 0: ";:GOSUB 1170:PRINT C0$
1730 IF B=-1THEN PRINT I$(4)"NO ENTRY\a!":X9=1:GOSUB4490:GOTO 1720
1740 IF B=0THEN GOSUB 2620:GOTO 1830
1750 IF (S=1AND B>6)OR (S>1AND B>7)THEN B=-1:GOTO 1730
1760 PRINT I$(4)"LINE";B;"SELECTED":X9=2:GOSUB4490:GOSUB 2620
1770 IF S(S,B)>12OR S(S,B)<-12THEN PRINT I$(4)"LINE";B;"CONTROLLED";:X9=2:GOSUB4490
1780 IF S(S,B)>12OR S(S,B)<-12THEN GOSUB 2620:RETURN 
1790 IF S$="h"OR S$="`"GOTO 1820
1800 IF H(S,B)>0THEN PRINT I$(5)"LINE SHIELDED\a!";:X9=2:GOSUB4490:GOSUB 2620:RETURN 
1810 IF S$="b"GOTO 1840
1820 X=0:GOSUB 1860
1830 GOSUB 110:RETURN 
1840 IF S(S,B)<0THEN H(S,B)=13-ABS(S(S,B)):GOTO 1820
1850 H(S,B)=13:GOTO 1820
1860 FOR Z=B TO B:ON S GOTO 1350,1360,1370,1380,1390
1870 REM 
1880 REM *** TO SHOW SHIELD STATUS (FROM LINE 1530) ***
1890 REM 
1900 PRINT I$(4)"SECTOR";S;"LINE";Z;":";I$(5);H(S,Z);"OF 13 SHIELD UNITS"
1910 PRINT I$(6)"REMAIN OPERATIVE":X9=2:GOSUB4490:GOSUB 2620:RETURN 
1920 REM 
1930 REM *** FOR FIRING ***
1940 REM 
1950 GOSUB 2620:PRINT I$(1)"FIRE CONTROL READY:"
1960 GOTO 1700
1970 IF S(S,Z)<0GOTO 2000:REM *** FROM LINE 1460 ***
1980 FOR A1=1TO S(S,Z):PRINT ">";CHR$(8);:X9=1:GOSUB4490:PRINT "\a*";:NEXT :PRINT J$;
1990 Y=Y+S(S,Z):PRINT I$(4)"ALLIES \aDESTROYED =";S(S,Z):X9=2:GOSUB4490:GOTO 2030
2000 FOR A1=1TO ABS(S(S,Z)):PRINT "~";:X9=1:GOSUB4490:PRINT CHR$(8);"\a*";:NEXT 
2010 PRINT J$
2020 Y=Y+ABS(S(S,Z)):PRINT I$(4)"ALIENS DESTROYED =";ABS(S(S,Z)):X9=2:GOSUB4490
2030 IF H(S,Z)>0THEN GOSUB 2620:PRINT I$(4)"SHIELD \aDESTROYED!":X9=2:GOSUB4490
2040 GOSUB 2620:S(S,Z)=0:H(S,Z)=0:PRINT J1$;
2050 RETURN 
2060 REM 
2070 REM *** FOR SCANNING A SECTOR ***
2080 REM 
2090 GOSUB 2620:PRINT I$(1)"SCANNER SELECTED:"
2100 GOSUB 710:GOTO 1700
2110 REM 
2120 REM *** TO DETERMINE HOW MANY ALIENS ATTACK (LINE 320) ***
2130 REM 
2140 IF P=2THEN R=6:GOTO 2220
2150 IF P=4THEN R=5:GOTO 2220
2160 IF P=7THEN R=5:GOTO 2220
2170 IF P=10THEN R=4:GOTO 2220
2180 IF P=13THEN R=4:GOTO 2220
2190 IF P=16THEN R=2:GOTO 2220
2200 IF P=19THEN R=2:GOTO 2220
2210 GOTO 2230
2220 R=INT(R*RND(1)+1):IF R>1THEN D$=C$:FOR R1=2TO R:C$=C$+D$:NEXT 
2230 RETURN 
2240 REM 
2250 REM *** TO DETERMINE HOW MANY ALLIES APPEAR (LINE 320) ***
2260 REM 
2270 IF P<5THEN R=2:GOTO 2220
2280 IF P=7THEN R=3:GOTO 2220
2290 IF P=10THEN R=3:GOTO 2220
2300 IF P=13THEN R=4:GOTO 2220
2310 IF P=16THEN R=4:GOTO 2220
2320 IF P=19THEN R=5:GOTO 2220
2330 GOTO 2230
2340 REM 
2350 REM *** GAME ACTION (HERE TO LINE 2410 AVAILABLE FOR MODIFICATIONS) ***
2360 REM 
2370 ON INT((P+4)/5)GOTO 2380,2390,2400,2410,2420
2380 GOSUB 110:GOSUB 110:GOSUB 110:GOSUB 110:GOSUB 110:GOSUB 910:GOTO 2380
2390 GOSUB 110:GOSUB 110:GOSUB 110:GOSUB 110:GOSUB 910:GOTO 2390
2400 GOSUB 110:GOSUB 110:GOSUB 110:GOSUB 910:GOTO 2400
2410 GOSUB 110:GOSUB 110:GOSUB 910:GOTO 2410
2420 GOSUB 110:GOSUB 910:GOTO 2420
2430 REM 
2440 REM *** TO INTRODUCE GAME AND ACTIVATE SECTORS ***
2450 REM 
2460 DIM L$(25),I$(8),S1$(7),S2$(7),S3$(7),S4$(7),S5$(7),I(5)
2470 FOR Z=1TO 5:I(Z)=1:NEXT Z:DIM S(5,7),H(5,7):P=15:PRINT H$"E"
2480 DIM O(5):GOSUB 2660:GOSUB 2620
2490 PRINT I$(1);G$;" ACTIVATE A SECTO\aR";I$(2);" (KEYS f1 TO f5)";F$;:A=1
2500 GOSUB 1200:IF ASC(A$)<83OR ASC(A$)>87GOTO 2490
2510 IF I(S)=0THEN PRINT I$(2)"\aALREADY ACTIVATED!":X9=2:GOSUB4490:GOSUB 2620
2520 IF I(S)=0GOTO 2490
2530 PRINT C0$
2540 I(S)=0:S$=" ":GOSUB 2620:GOSUB 1220:FOR Z=1TO 5:IF I(Z)=1GOTO 2560
2550 NEXT :GOTO 2600
2560 PRINT I$(1);C1$;"ACTIVATE ANOTHER (\aY/N)?";:GOSUB 1130:PRINT C0$
2570 GOSUB 2620
2580 IF A$<>"Y"AND A$<>"N"THEN PRINT I$(3)"\aNO ENTRY!":GOTO 2560
2590 IF A$="Y"GOTO 2490
2600 PRINT I$(1)"OPERATIONAL CONTROL";I$(2)"TRANSFER COMPLETE\a.";:X9=4:GOSUB4490
2610 PRINT Y$:GOSUB 2620:GOTO 2340
2620 REM 
2630 REM *** TO ERASE LINES IN TOP CENTER OF DISPLAY ***
2640 REM 
2650 FOR E=1TO 8:PRINT Q$;I$(E);SPC(24):NEXT :RETURN 
2660 REM 
2670 REM *** TO PREPARE TERMINAL OPERATOR (SECTOR CONTROLLER) ***
2680 REM 
2690 PRINT H$"E"
2700 PRINT TAB(20)"STRATEGIC SPACE COMMAND"
2710 PRINT TAB(20)"QUADRANT I, SECTORS 1-5":PRINT 
2720 PRINT TAB(20)" TERMINAL POSITION `A\a'":PRINT :X9=4:GOSUB4490
2730 PRINT "FOR OPERATIONAL USE, THIS POSITION REQUIRES AN H19/H89 TERMINAL"
2740 PRINT "RUNNING AT 9600 BAUD OR FASTER, PROGRAM INITIATION VIA MBASIC."
2750 PRINT "THE CAPS LOCK MUST BE ON (DOWN).  IF YOUR TERMINAL OR LANGUAGE"
2760 PRINT "DOES NOT MEET THESE SPECS, INITIATE CONTROL-C TERMINATION,
2770 PRINT "RECONFIGURE, AND RESTART THIS PROGRAM..."
2780 PRINT "(CTRL-C TO TERMINATE; SPACE BAR TO GO ON...)";:XY$=INPUT$(1)
2790 PRINT H$"E";CHR$(7):PRINT :PRINT 
2800 PRINT :PRINT "TERMINAL READY...":X9=2:GOSUB4490:PRINT :PRINT 
2810 GOSUB 3710:PRINT 
2820 PRINT "SENIOR CONTROLLER NOTIFIED.  STAND BY...";:X9=4:GOSUB4490
2830 GOSUB 3810:PRINT E0$:PRINT "FROM: SENIOR CONTROLLER":PRINT "TO: `A'"
2840 PRINT :PRINT "HAVE YOU BEEN BRIEFED (Y/N)?":PRINT :PRINT "FROM: `A'"
2850 PRINT "TO: SENIOR CONTROLLER":PRINT :PRINT "RESPONSE: ";:GOSUB 1130
2860 IF A$<>"Y"THEN GOSUB 2990:REM *** INSTRUCTIONS ***
2870 PRINT H$"E";CHR$(7):PRINT :PRINT 
2880 PRINT :PRINT "FROM: SENIOR CONTROLLER":PRINT "TO: `A'":PRINT 
2890 PRINT "WHAT IS YOUR CONTROLLER RANK (1-9)?":PRINT 
2900 PRINT "FROM: `A'":PRINT "TO: SENIOR CONTROLLER":PRINT 
2910 PRINT "RESPONSE (1-9): ";:GOSUB 1170:IF B=0OR B=-1GOTO 2880
2920 P=28-(B*3):IF P=1THEN P=2:REM *** DEGREE OF DIFFICULTY ***
2930 PRINT H$"E":PRINT "FROM: SENIOR CONTROLLER":PRINT "TO: `A'":PRINT 
2940 IF B>5THEN PRINT "GOOD!  WE NEED SOME EXPERIENCE ON THESE TERMINALS."
2950 IF B<6AND B>2THEN PRINT "OK.  THINGS ARE QUIET NOW, BUT STAY ALERT!"
2960 IF B<3THEN PRINT "IS THAT ALL?!  WELL, I HOPE YOU CAN HACK IT."
2970 X9=6:GOSUB4490:PRINT H$"E":PRINT "TRANSFERRING OPERATIONAL CONTROL...";
2980 PRINT CHR$(7):X9=4:GOSUB4490:PRINT CHR$(7):X9=2:GOSUB4490:PRINT E0$:GOTO 800
2990 REM 
3000 REM *** INSTRUCTIONS ***
3010 REM 
3020 PRINT E0$:GOSUB 800:PRINT I$(2)"STUDY THIS DISPLAY";I$(3);C1$;
3030 PRINT "THEN PRESS SPACE BAR";I$(4)"TO CONTINUE BRIEFING...";:XY$=INPUT$(1) 
3040 PRINT G$
3050 PRINT H$"E";SPC(13)"STRATEGIC SPACE COMMAND HEADQUARTERS":PRINT 
3060 PRINT TAB(9)"COMMAND POST BRIEFING FOR SECTOR CONTROLLERS":PRINT :PRINT 
3070 PRINT "THIS TERMINAL CONTROLS SECTORS 1 THRU 5 OF SPACE QUADRANT I."
3080 PRINT "AS A SECTOR CONTROLLER, YOU ARE RESPONSIBLE FOR MONITORING"
3090 PRINT "THE ACTIVITY IN THOSE SECTORS, KEEPING TRACK OF THE LOCATION"
3100 PRINT "OF ALIEN & ALLIED SHIPS, AND DESTROYING ANY ALIENS IN YOUR"
3110 PRINT "SECTORS.  YOUR TERMINAL FUNCTION KEYS, f1 TO f5, ARE USED TO"
3120 PRINT "SELECT A SECTOR, AND THE THREE COLORED FUNCTIONS KEYS ARE"
3130 PRINT "USED TO ACTIVATE ELECTRONIC SHIELDING (BLUE), FIRE WEAPONS"
3140 PRINT "(RED), OR SCAN (GREY) WITHIN THE SELECTED SECTOR."
3150 PRINT "YOUR ACTIONS, THEN, ARE THESE:"
3160 PRINT "- REMEMBER THE LOCATIONS OF ALIENS AND ALLIES"
3170 PRINT "- SELECT A SECTOR WITH ONE OF THE 5 'f' KEYS WHILE THE
3180 PRINT "  INDICATOR ABOVE THEM IS FLASHING (ON LINE 25).
3190 PRINT "- WHEN YOUR SELECTION IS ACKNOWLEDGED, USE A COLORED FUNCTION
3200 PRINT "  KEY TO SHIELD, FIRE, OR SCAN IN THAT SECTOR, WHILE THE
3210 PRINT "  INDICATOR ABOVE THE COLORED KEYS IS FLASHING.
3220 PRINT "- WHEN ASKED, ENTER A LINE IN THE SELECTED SECTOR.
3230 PRINT "  NOTE THAT THERE ARE 6 LINES IN SECTOR 1 AND 7 IN THE OTHERS.
3240 PRINT "(PRESS SPACE BAR TO CONTINUE THE BRIEFING)";:XY$=INPUT$(1) 
3250 PRINT H$"E":PRINT "THE GALAXY IS UNDER HEAVY ATTACK."
3260 PRINT "ALIENS MUST BE DESTROYED BY FIRING ON THE LINE THEY OCCUPY."
3270 PRINT "IF ALLOWED TO ACCUMULATE 13 SHIPS ON A LINE, THEY WILL CONTROL"
3280 PRINT "THAT LINE IRREVOCABLY -- AND IF THEY CONTROL 4 LINES IN A"
3290 PRINT "SECTOR, THEY CONTROL THE SECTOR.  ALLIED SHIPS WILL DESTROY"
3300 PRINT "SOME OF THEM ON A ONE-FOR-ONE BASIS, BUT COMMAND INTELLIGENCE"
3310 PRINT "INDICATES THAT THE ALLIES ARE OUTNUMBERED BY AS MUCH AS 5 TO 1."
3320 PRINT "THEREFORE, IT IS VITALLY IMPORTANT THAT YOU KNOW HOW MANY"
3330 PRINT "ALLIES AND/OR ALIENS ARE ON THE LINE ON WHICH YOU WILL FIRE"
3340 PRINT "BEFORE - REPEAT, BEFORE - YOU FIRE.  IT IS RECOGNIZED THAT THE"
3350 PRINT "IMMENSE FORCE GENERATED BY FIRING WILL DESTROY ALL - REPEAT,"
3360 PRINT "ALL - SHIPS ON THE LINE FIRED UPON, AND ALLIED LOSSES FROM"
3370 PRINT "FRIENDLY FIRE ARE FREQUENTLY UNAVOIDABLE; BUT YOU SHOULD USE"
3380 PRINT "EXTREME CARE - FIRING ONLY WHEN ALIEN LOSSES WILL EXCEED ALLIED"
3390 PRINT "LOSSES ON THAT LINE.  SOME ALLIED SHIPS ARE SIMILAR IN APPEARANCE"
3400 PRINT "TO SOME ALIEN SHIPS; SO, AGAIN, USE EXTREME CARE WHEN FIRING."
3410 PRINT "WE RECOMMEND SCANNING A LINE BEFORE FIRING ON IT - IF YOU CAN"
3420 PRINT "SPARE THE TIME TO DO SO. THE USE OF SHIELDING - PREFERABLY BEFORE"
3430 PRINT "ALIENS OCCUPY THE LINE - MAY BE ADVISABLE TO BUY SOME TIME. AN"
3440 PRINT "ALIEN ENTERING A SHIELDED AREA WILL BE DESTROYED - BUT SO WILL"
3450 PRINT "THAT PORTION OF THE SHIELD.  FIRING WILL ALSO DESTROY THE SHIELD."
3460 PRINT "(SPACE BAR TO CONTINUE...)";:XY$=INPUT$(1) :PRINT H$"E"
3470 GOSUB 4190:PRINT H$"E"
3480 PRINT "IF YOU ARE INEXPERIENCED (CONTROLLER RANKS 1 AND 2),"
3490 PRINT "THE SENIOR CONTROLLER WILL INSURE THAT OTHER TERMINALS ALSO"
3500 PRINT "MONITOR YOUR SECTORS - AND YOUR JOB WILL BE LESS DIFFICULT."
3510 PRINT "ALSO, YOU MAY ACTIVATE FEWER THAN 5 OF THE SECTORS AND THOSE"
3520 PRINT "NOT ACTIVATED WILL BE CONTROLLED BY OTHERS.  THIS PROCEDURE"
3530 PRINT "IS NOT - REPEAT, NOT - RECOMMENDED UNLESS THIS IS YOUR FIRST"
3540 PRINT "DUTY DAY.  FAILURE TO ACTIVATE ALL 5 SECTORS WILL BE NOTED"
3550 PRINT "ON THE CONTROLLER PERFORMANCE REPORT OF THOSE ABOVE RANK 1."
3560 PRINT "YOU WILL BE ASKED FOR YOUR CONTROLLER RANK AFTER THE BRIEFING."
3570 PRINT:PRINT "IF ALL SECTORS SELECTED COME UNDER ALLIED CONTROL, YOUR
3580 PRINT "MISSION IS OVER AND YOU HAVE SAVED THE GALAXY.  IF ALIENS TAKE
3590 PRINT "OVER ALL SECTORS, THE GALAXY IS LOST AND YOUR MISSION IS TERMINATED.
3600 PRINT :PRINT "UNCLASSIFIED BRIEFING CONCLUDED.  INSURE THAT THE"
3610 PRINT "IMMEDIATE AREA AROUND YOUR TERMINAL IS SECURE AND THEN PRESS"
3620 PRINT "SPACE BAR FOR CLASSIFIED BRIEFING...";:XY$=INPUT$(1) 
3630 PRINT E0$:PRINT TAB(20)"\a*** TOP SECRET ***":PRINT 
3640 PRINT "SINGLE LETTER COMMAND TO FIX A MALFUNCTIONING SECTOR IS:"
3650 X9=4:GOSUB4490
3660 PRINT TAB(28);:FOR Z=65TO 90:PRINT CHR$(Z);CHR$(8);:FOR II=1 TO 10:NEXT:NEXT 
3670 FOR Z=65TO 90:PRINT CHR$(Z);:X9=1:GOSUB4490:PRINT CHR$(8);:NEXT :PRINT " "
3680 PRINT H$"E":PRINT :PRINT "COMMAND CODE WITHHELD\a!";:X9=4:GOSUB4490
3690 PRINT :PRINT "TERMINAL OPERATOR'S CLEARANCE LIMITED TO `SECRET'\a."
3700 X9=6:GOSUB4490:PRINT H$"E":RETURN 
3710 REM
3720 REM *** TO ESTABLISH VARIABLES FOR MODES OF OPERATION ***
3730 REM 
3740 E$=H$:F$=E$+"F":G$=E$+"G":Y=31:FOR Z=1TO 25:Y=Y+1
3750 L$(Z)=E$+"Y"+CHR$(Y)+" ":NEXT :PRINT CHR$(7);:C0$=E$+"x5":C1$=E$+"y5"
3760 X$=E$+"x1":Y$=E$+"y1":P$=E$+"p":Q$=E$+"q":J$=E$+"j":J1$=E$+"k":K$=E$+"K"
3770 Y=31:FOR Z=1TO 8:Y=Y+1:I$(Z)=E$+"Y"+CHR$(Y)+CHR$(60):NEXT 
3780 Y=32:FOR Z=1TO 7:Y=Y+1:S2$(Z)=E$+"Y"+CHR$(Y)+"$":NEXT :PRINT CHR$(7)
3790 Y=32:FOR Z=1TO 7:Y=Y+1:S3$(Z)=E$+"Y"+CHR$(Y)+"U":NEXT 
3800 RETURN 
3810 Y=40:FOR Z=1TO 6:Y=Y+1:S1$(Z)=E$+"Y"+CHR$(Y)+"<":NEXT :PRINT CHR$(7)
3820 Y=47:FOR Z=1TO 7:Y=Y+1:S4$(Z)=E$+"Y"+CHR$(Y)+"$":NEXT 
3830 Y=47:FOR Z=1TO 7:Y=Y+1:S5$(Z)=E$+"Y"+CHR$(Y)+"U":NEXT :PRINT CHR$(7)
3840 E0$=E$+"E":RETURN 
3850 REM 
3860 REM *** TO PRINT SCREEN DISPLAY ***
3870 REM 
3880 PRINT C0$
3890 PRINT J1$;G$:PRINT L$(10);SPC(11);"^SECTOR 2^";SPC(40);"^SECTOR 3^";F$
3900 PRINT L$(15);SPC(11);"kSECTOR 4k";SPC(40);"kSECTOR 5k";G$;CHR$(7)
3910 PRINT L$(17);SPC(35);"^SECTOR 1^";F$
3920 PRINT L$(1);"   f";:FOR Z=5TO 27:PRINT "a";:NEXT :PRINT "c"
3930 FOR Z=2TO 8:PRINT L$(Z);"   `":NEXT :PRINT L$(9);"   e";:FOR Z=5TO 27
3940 PRINT "a";:NEXT :PRINT CHR$(7):FOR Z=33TO 39:PRINT E$;"Y";CHR$(Z);
3950 PRINT CHR$(59);"`";:NEXT :PRINT E$;"Y";CHR$(40);CHR$(59);"b"
3960 PRINT E$;"Y Tf";:FOR Z=54TO 76:PRINT "a";:NEXT :PRINT "c"
3970 FOR Z=33TO 39:PRINT E$;"Y";CHR$(Z);"T`":NEXT :PRINT E$;"Y(Tb";CHR$(7)
3980 PRINT E$;"Y(U";:FOR Z=54TO 76:PRINT "a";:NEXT :PRINT "d"
3990 FOR Z=33TO 39:PRINT E$;"Y";CHR$(Z);CHR$(108);"`":NEXT 
4000 FOR Z=41TO 46:PRINT E$;"Y";CHR$(Z);CHR$(59);"`";SPC(24);"`":NEXT 
4010 PRINT L$(16);"   f";SPC(23);"b";SPC(24);"b";SPC(23);"c";CHR$(7)
4020 FOR Z=17TO 23:PRINT L$(Z);"   `";SPC(23);"`":NEXT 
4030 FOR Z=48TO 54:PRINT E$;"Y";CHR$(Z);"T`";SPC(23);"`":NEXT 
4040 PRINT L$(24);"   e";:FOR Z=36TO 58:PRINT "a";:NEXT :PRINT "d";
4050 PRINT E$;"Y/$";:FOR Z=36TO 58:PRINT "a";:NEXT :PRINT CHR$(7)
4060 PRINT E$;"Y7Te";:FOR Z=85TO 107:PRINT "a";:NEXT :PRINT "d";L$(1)
4070 PRINT E$;"Y/<";:FOR Z=1TO 24:PRINT "a";:NEXT :PRINT CHR$(7)
4080 PRINT E$;"Y/U";:FOR Z=1TO 23:PRINT "a";:NEXT 
4090 PRINT E$;"Y(<";:FOR Z=1TO 24:PRINT "a";:NEXT :PRINT CHR$(7)
4100 PRINT E$;"Y2=ALLIED SHIPS = j  >"
4110 PRINT E$;"Y3=ALIEN SHIPS  = g ~ ^"
4120 PRINT E$;"Y5=ALIEN CONTROL = A";E$;"Y6=ALLIED CONTROL = F"
4130 FOR Z=2TO 8:PRINT L$(Z);Z-1:NEXT 
4140 FOR Z=10TO 15:PRINT E$;"Y";CHR$(Z+31);"8";Z-9:NEXT 
4150 FOR Z=17TO 23:PRINT L$(Z);Z-16:NEXT 
4160 FOR Z=1TO 7:PRINT E$;"Y";CHR$(Z+32);"m";Z:NEXT 
4170 FOR Z=1TO 6:PRINT E$;"Y";CHR$(Z+40);"U";Z:NEXT 
4180 FOR Z=1TO 7:PRINT E$;"Y";CHR$(Z+47);"m";Z:NEXT :PRINT L$(10):RETURN 
4190 REM 
4200 REM *** ADDITIONAL INSTRUCTIONS ***
4210 REM 
4220 PRINT CHR$(7);SPC(20)"*** BULLETIN ***":PRINT 
4230 PRINT "A BUG IN THE SOFTWARE WILL CAUSE CERTAIN ON-SCREEN OPERATIONS"
4240 PRINT "TO APPEAR TO DESTROY PORTIONS OF AN ACTIVE SHIELD. THIS IS"
4250 PRINT "ONLY ILLUSORY. THE SHIELD CAN ONLY BE DESTROYED BY FIRING"
4260 PRINT "INTO IT AND WHEN ONE OR MORE ALIENS APPEAR IN A SHIELDED SPACE,"
4270 PRINT "IN WHICH CASE BOTH THAT PORTION OF THE SHIELD AND THE ALIEN(S)"
4280 PRINT "WILL BE DESTROYED. IF THE SHIELD APPEARS TO BE DESTROYED BY"
4290 PRINT "ANY OTHER OCCURENCE, DISREGARD IT. YOU MAY CONFIRM THE"
4300 PRINT "PRESENCE OR ABSENCE OF SHIELDING AT ANY TIME BY USING THE"
4310 PRINT "SCAN FUNCTION. THIS SAME ANOMOLY SOMETIMES OCCURS IN CONNEC-"
4320 PRINT "TION WITH THE `A' AND `F' SYMBOLS WHICH INDICATE ALIEN OR"
4330 PRINT "ALLIED CONTROL OF A GIVEN LINE. WHENEVER THESE OR SIMILAR"
4340 PRINT "PROBLEMS OCCUR, USE THE SCAN FUNCTION TO DETERMINE THE TRUE"
4350 PRINT "STATUS OF A SECTOR OR LINE.  THE COMMAND LOGISTICS DIVISION"
4360 PRINT "ESTIMATES THAT THE SOFTWARE CONTRACTOR WILL HAVE A PROGRAM"
4370 PRINT "MODIFICATION ENTERED DURING THE DOWN-TIME SCHEDULED FOR LATER"
4380 PRINT "THIS YEAR.  +++ \aEND OF BULLETIN +++"
4390 PRINT :PRINT "(SPACE BAR TO CONTINUE BRIEFING...)";:XY$=INPUT$(1) :RETURN 
4400 REM
4410 REM *** SET UP KEY SCAN ***
4420 REM
4430 DIM U0(3):U0(0)=&H36:U0(1)=&H1FF:U0(2)=&H77D8:U0(3)=&HC9
4440 DEF USR0=VARPTR(U0(0))
4450 IF USR0(0)<>0 THEN 4450 :ELSE RETURN
4460 REM
4470 REM *** PAUSE ROUTINE ***
4480 REM
4490 POKE 8219,0:POKE 8220,0
4500 IF PEEK(8220)=X9 THEN RETURN :ELSE 4500
