10 REM ******************* WORD SEARCH *******************
20 REM 
30 REM\t\t   C. F. MOWERY, JR.  MODIFIED BY P. SWAYNE
40 REM\t\t   13305 S. 26th AVE.
50 REM\t\t   OMAHA, NE   68123
60 REM\t\t   TEL: (402) 292-6344
70 CLEAR 1000
80 DEFINT A-Z:DIM A$(6),A1$(2),A2$(2),A3$(2),A4$(4),A5$(1),A6$(2),L$(9):D=11
90 DIM J$(4),K$(4),P$(6),W$(5),W1$(5,13),C(12),L(5),M(5),P(13),S(5):O=1
100 DIM P0$(5),P1$(5),P2$(5),P3$(5),P4$(5),P5$(5):B$=CHR$(27):GOSUB 4980 
110 GOSUB4260:GOSUB3770
120 PRINTB$"Y+7";K$;"HOW MANY PLAYERS ";C1$;"? ";:Z$=INPUT$(1):PRINT Z$:X=VAL(Z$):PRINT
130 IF X>3THEN PRINTTAB(20)"LIMIT: 3":PRINT:GOTO120
140 IF X=0THEN PRINTTAB(20)"I CAN'T PLAY BY MYSELF!!":PRINT:GOTO120
150 N=X+1:PRINTB$"Y+-";K$;"OK, THAT'S";N;"PLAYERS, COUNTING ME...";
160 X9=4:GOSUB5030:T=N-1:PRINTE$;Y$
170 PRINTTAB(10)"AFTER TYPING A PLAYER'S NAME, PRESS THE 'RETURN' KEY."
180 PRINT:PRINT:FOR Z=1TO N-1
190 PRINTTAB(20)"FIRST NAME OF PLAYER";Z;
200 LINE INPUT "\a? ";P$(Z):IF LEN(P$(Z))>9THEN M=1:PRINT
210 IF M=1THEN PRINTTAB(20)"\a\a9 LETTER LIMIT. TRY AGAIN: ";:M=0:GOTO200
220 PRINT:PRINT:NEXT Z:P$(N)="COMPUTER":PRINTE$:GOSUB4930:PRINTL$(1);
230 PRINT"PLAYERS:",:FOR Z=1TO N:PRINTZ;P$(Z),:NEXT Z:PRINT:PRINT
240 PRINTSPC(20)"IS THAT RIGHT? ";:Z$=INPUT$(1):PRINTZ$;:PRINTE$:IF Z$<>"Y"GOTO120
250 PRINTY$
260 PRINT"NOW, EACH OF YOU MUST TYPE YOUR WORD, WITH OR WITHOUT BLANKS."
270 PRINT"TRUST ME. I PROMISE NOT TO LOOK AT IT. BUT . . . . . . . . . ."
280 PRINT"MAKE SURE NO ONE ELSE IS WATCHING THE SCREEN!":PRINT
290 PRINT"PRESS 'RETURN' WHEN YOU ARE READY TO BEGIN. . .";:LINE INPUT XY$ 
300 PRINTE$:FOR Z=1TO N-1
310 PRINT"PRESS THE 'RETURN' KEY WHEN YOU FINISH TYPING YOUR WORD"
320 PRINT"OR WHEN YOU MAKE A MISTAKE. I WILL GIVE YOU A CHANCE TO EITHER"
330 PRINT"CORRECT MISTAKES OR CHANGE YOUR WORD.  REMEMBER: NO HYPHENS,"
340 PRINT"APOSTROPHES, PROPER NAMES. NO BLANKS IN THE MIDDLE OF THE WORD."
350 PRINT:PRINTP$(Z);", TYPE YOUR WORD HERE: ";
360 M=0:LINE INPUT " ";W$(Z):IF LEN(W$(Z))>12THEN M=1:PRINT
370 IF M=1THEN M=0:PRINT"12 LETTER LIMIT. TRY AGAIN:";:GOTO360
380 FOR Y=1TO LEN(W$(Z)):IF MID$(W$(Z),Y,1)="*"THEN M=M+1
390 NEXT Y:IF M>5THEN M=0:PRINT"5 BLANK LIMIT. TRY AGAIN:";:GOTO360
400 M=O:GOSUB4930:PRINTL$(1);K$;"CHECK YOUR SPELLING. WANT TO CHANGE IT? ";
410 Z$=INPUT$(1):PRINTZ$;:IF Z$<>"N"THEN PRINTK$;"OK. TRY AGAIN HERE: ";:GOTO360
420 M=0:FOR Y=1TO LEN(W$(Z)):IF MID$(W$(Z),Y,1)=" "THEN M=1
430 IF M=1THEN PRINT"WAIT\a! YOUR WORD HAS A SPACE IN IT. YOU MUST HAVE"
440 IF M=1THEN M=0:PRINT"PRESSED THE SPACE BAR. TRY AGAIN:";:GOTO360
450 NEXT Y:FOR Y=1TO LEN(W$(Z)):W1$(Z,Y)="i":NEXT Y
460 PRINTE$:NEXT Z
470 PRINTE$;Y$;B$"Y+ NOW I'LL SELECT A WORD. IT MAY TAKE A FEW MOMENTS."
480 OPEN "I",1,DK$+"WORDS.DAT"
490 L=INT(10*RND(1)+3):B=INT(6*RND(1)):IF L+B>12GOTO490
500 IF L>7GOTO570
510 FOR Z=1TO 500:LINE INPUT #1,A$:IF LEN(A$)=L THEN Y=INT(100*RND(1)+1):GOTO540
520 PRINT".";:IF Z=80 OR Z=160 THEN PRINT
530 NEXT Z
540 FOR Z=1TO 100:IF Y=Z THEN W$(N)=A$:GOTO610
550 PRINT".";:IF Z=80 OR Z=160 THEN PRINT
560 LINE INPUT #1,A$:NEXT Z
570 FOR Z=1TO 500:LINE INPUT #1,A$:PRINT".";:IF Z=80 OR Z=160 THEN PRINT
580 NEXT Z:Y=INT(6*RND(1)+1):FOR Z=1TO 25:LINE INPUT #1,A$:IF LEN(A$)<>L THEN NEXT Z
590 FOR Z=1TO Y:IF Z=Y THEN W$(N)=A$:GOTO610
600 LINE INPUT #1,A$:NEXT Z
610 CLOSE #1 :IF B=0GOTO730
620 A=INT(2*RND(1)):F=INT(2*RND(1)):IF A=0GOTO660
630 IF F=1GOTO650
640 FOR Z=1TO B:W$(N)="*"+W$(N):NEXT Z:GOTO730
650 FOR Z=1TO B:W$(N)=W$(N)+"*":NEXT Z:GOTO730
660 A=INT(B*RND(1)+1):IF F=1GOTO680
670 FOR Z=1TO A:W$(N)="*"+W$(N):NEXT Z:GOTO690
680 FOR Z=1TO A:W$(N)=W$(N)+"*":NEXT Z
690 A=B-A:IF A=0GOTO730
700 IF F=1GOTO720
710 FOR Z=1TO A:W$(N)=W$(N)+"*":NEXT Z:GOTO730
720 FOR Z=1TO A:W$(N)="*"+W$(N):NEXT Z
730 FOR Z=1TO N:FOR Y=1TO LEN(W$(Z)):W1$(Z,Y)="i":NEXT Y:NEXT Z
740 P(1)=2:P(6)=2:P(7)=2:P(12)=2:P(2)=4:P(5)=4:P(8)=4:P(11)=4
750 P(3)=6:P(4)=6:P(9)=6:P(10)=6
760 A$(0)="A":A$(1)="E":A$(2)="I":A$(3)="O":A$(4)="T":A$(5)="N":A$(6)="S"
770 A1$(0)="R":A1$(1)="L":A1$(2)="D":A2$(0)="H":A2$(1)="C":A2$(2)="U"
780 A3$(0)="P":A3$(1)="F":A3$(2)="M":A4$(0)="W":A4$(1)="Y":A4$(2)="B"
790 A4$(3)="G":A4$(4)="V":A5$(0)="K":A5$(1)="Q":A6$(0)="X":A6$(1)="J"
800 A6$(2)="Z"
810 FOR Z=1TO 3:J$(Z)="ETAONISRLDPFMBGETAONISRLDHCUPFMWYBGVKQXJZ":NEXT Z
820 PRINT:PRINT"OK. I HAVE A WORD... ":X9=2:GOSUB5030:PRINTE$:GOSUB3450
830 IF C=1THEN C=0:O=1:M=1:PRINTK$;"I'M COMPUTING . . .";:GOTO880
840 IF E=1THEN E=0:O=2:PRINTK$;"STAND BY WHILE I THINK . . .";:GOTO860
850 T=T+1:PRINTL$(1);K$;"ONE MOMENT, PLEASE . . .";:IF T=N+1THEN T=1
860 GOSUB3190:GOSUB3260:GOSUB3290:GOSUB3530:GOSUB3750
870 IF M=0THEN GOSUB910
880 IF M=1THEN M=0:GOSUB3190:GOSUB3260:GOSUB3290:GOSUB3530:GOSUB3750
890 IF L(T)=0AND Q>0THEN V=0:GOTO830
900 GOSUB1550:GOTO830
910 IF D=11THEN GOSUB4170:FOR Z=1TO 11:C(Z)=0:NEXT Z:D=0
920 PRINT"DRAW CARD READS: ";
930 IF D=11THEN PRINT:GOTO910
940 X=INT(48*RND(1)+1):IF X<38GOTO1100
950 ON X-37GOTO960,960,980,980,1000,1000,1020,1040,1060,1080,1080
960 IF C(5)=2THEN D=D+1:GOTO930
970 C(5)=C(5)+1:V=3:PRINT"TRIPLE VALUE OF FIRST GUESS!":GOTO1510
980 IF C(6)=2THEN D=D+1:GOTO930
990 C(6)=C(6)+1:V=4:PRINT"QUADRUPLE VALUE OF FIRST GUESS!":GOTO1510
1000 IF C(7)=2THEN D=D+1:GOTO930
1010 C(7)=C(7)+1:V=5:PRINT"QUINTUPLE VALUE OF FIRST GUESS!":GOTO1510
1020 IF C(8)=1THEN D=D+1:GOTO930
1030 C(8)=C(8)+1:S(T)=S(T)+15:PRINT"ADD 15 TO SCORE.":GOTO1510
1040 IF C(9)=1THEN D=D+1:GOTO930
1050 C(9)=C(9)+1:S(T)=S(T)+20:PRINT"ADD 20 TO SCORE.":GOTO1510
1060 IF C(10)=1THEN D=D+1:GOTO930
1070 C(10)=C(10)+1:S(T)=S(T)+25:PRINT"ADD 25 TO SCORE!":GOTO1510
1080 IF C(11)=2THEN D=D+1:GOTO930
1090 C(11)=C(11)+1:S(T)=S(T)-10:PRINT"DEDUCT 10 POINTS!":GOTO1510
1100 IF X<16AND C(1)<15THEN PRINT"TAKE NORMAL TURN.":C(1)=C(1)+1:GOTO1510
1110 IF X<16THEN D=D+1:GOTO930
1120 IF X<22AND C(2)<6THEN PRINT"EXTRA TURN!":C(2)=C(2)+1:E=1:GOTO1510
1130 IF X<22THEN D=D+1:GOTO930
1140 IF X>31GOTO1230
1150 IF C(3)>9THEN D=D+1:GOTO930
1160 A=INT(N*RND(1)+1):IF A=T GOTO1160
1170 C(3)=C(3)+1:PRINT"PLAYER";A;"EXPOSE A LETTER OR BLANK":IF A=N GOTO1430
1180 GOSUB3190:IF L(A)=1GOTO1330
1190 PRINT"WHICH ONE";:GOSUB3690
1200 IF X=0THEN PRINT"TRY AGAIN.":GOTO1190
1210 IF W1$(A,X)<>"i"THEN PRINT"THAT'S BEEN EXPOSED.":GOTO1190
1220 W1$(A,X)=MID$(W$(A),X,1):S(T)=S(T)+P(X):M=1:P1=1:A$=W1$(A,X):GOTO1510
1230 IF C(4)>5THEN D=D+1:GOTO930
1240 C(4)=C(4)+1:PRINT"IF YOU HAVE A BLANK, EXPOSE IT.":IF T=N GOTO1340
1250 FOR Z=1TO LEN(W$(T))
1260 IF MID$(W$(T),Z,1)="*"AND W1$(T,Z)="i"GOTO1280
1270 NEXT Z:A=T:GOTO1330
1280 PRINT"POSITION OF BLANK (00=NONE) ";:GOSUB3690:IF X>0GOTO1300
1290 PRINT"CHECK AGAIN.":GOTO1280
1300 IF MID$(W$(T),X,1)<>"*"THEN PRINT"COUNT AGAIN.":GOTO1280
1310 IF W1$(T,X)="*"THEN PRINT"ALREADY EXPOSED.":GOTO1280
1320 W1$(T,X)="*":M=1:GOTO1510
1330 PRINTP$(A);" HAS NONE UNEXPOSED.":GOTO1510
1340 FOR Z=1TO LEN(W$(N)):IF W1$(N,Z)="i"AND MID$(W$(N),Z,1)="*"GOTO1360
1350 NEXT Z:GOTO1420
1360 PRINT"I'LL EXPOSE IT, BUT FIRST . . .":FOR Z=0TO LEN(W$(N))
1370 IF MID$(W$(N),Z+1,1)="*"AND W1$(N,Z+1)="i"GOTO1400
1380 IF MID$(W$(N),LEN(W$(N))-Z,1)="*"AND W1$(N,LEN(W$(N))-Z)="i"GOTO1410
1390 NEXT Z
1400 W1$(N,Z+1)="*":GOTO1510
1410 W1$(N,LEN(W$(N))-Z)="*":GOTO1510
1420 PRINT"SORRY, NO HIDDEN BLANKS.":GOTO1510
1430 PRINT"LET'S SEE . . .";:X9=2:GOSUB5030:PRINT
1440 FOR Z=1TO LEN(W$(N)):IF W1$(N,Z)="i"GOTO1460
1450 NEXT Z:PRINT:PRINT"I HAVE NO UNEXPOSED LETTERS.":X9=4:GOSUB5030:GOTO1510
1460 X=INT(LEN(W$(N))*RND(1)+1):IF X=1OR X=LEN(W$(N))GOTO1480
1470 IF MID$(W$(N),X,1)="*"AND W1$(N,X)="i"GOTO1490
1480 IF W1$(N,X)="i"THEN W1$(N,X)=MID$(W$(N),X,1):S(T)=S(T)+P(X):M=1:GOTO1510
1490 IF W1$(N,1)="i"OR W1$(N,LEN(W$(N)))="i"GOTO1460
1500 GOTO1460
1510 X=0
1520 IF P1=1OR M=1THEN PRINT"JUST A SEC . . .THESE DRAW CARDS CONFUSE ME..."
1530 IF P1=1GOTO2080
1540 RETURN 
1550 IF T=N GOTO1750
1560 PRINTP$(T);", ";:IF N=2THEN X=2:GOTO1630
1570 X=0:L1=0:GOTO1670
1580 PRINT"# OF THE WORD YOU'RE PROBING: ";:GOSUB3640:GOSUB5000:X=VAL(Z$):PRINT
1590 IF X=0THEN PRINT"TURN FORFEITED.  ";:GOTO3100
1600 IF X>N THEN PRINT"THERE ARE ONLY";N;"PLAYERS. ";:GOTO1580
1610 IF X=T THEN PRINT"THAT'S YOUR NUMBER. ";:GOTO1580
1620 IF L(X)=1THEN PRINT"THAT WORD IS FINISHED.";:GOTO1580
1630 PRINT"LETTER (OR *) YOU'RE GUESSING? ";:GOSUB3640:GOSUB5000:PRINT
1640 IF Q2=0 THEN GOTO2930
1650 IF (ASC(Z$)>64AND ASC(Z$)<91)OR ASC(Z$)=42GOTO2930
1660 PRINT"A-Z OR * ONLY!";:X9=2:GOSUB5030:GOSUB3750:GOTO1630
1670 FOR Z=1TO N:IF L(Z)=1THEN L1=1
1680 NEXT Z:IF L(T)=1AND L1=N-1THEN FOR Z=1TO N:IF L(Z)=0THEN X=Z:L1=0
1690 IF X>0GOTO1630
1700 IF L(T)=1AND L1=N-1THEN NEXT Z
1710 IF L(T)=0AND L1=N-2THEN FOR Z=1TO N:IF L(Z)=0AND Z<>T THEN X=Z:L1=0
1720 IF X>0GOTO1630
1730 IF L(T)=0AND L1=N-2THEN NEXT Z
1740 L1=0:GOTO1580
1750 PRINT"MY TURN. ";
1760 FOR Z=1TO N-1:IF L(Z)=1GOTO1790
1770 FOR Y=1TO LEN(W$(Z)):IF W1$(Z,Y)<>"i"THEN M(Z)=M(Z)+1
1780 NEXT Y
1790 NEXT Z:X=MAX(M(1),M(2),M(3)):FOR Z=1TO N-1
1800 IF M(Z)=X THEN X=Z:IF X=1AND M(1)=0THEN X=INT((N-1)*RND(1)+1):GOTO1820
1810 NEXT Z:IF X=1AND M(1)=0THEN X=INT((N-1)*RND(1)+1)
1820 FOR Z=1TO N-1:M(Z)=0:NEXT Z:IF X=0 THEN X=1
1830 PRINTP$(X);", LET'S SEE IF YOU HAVE ";
1840 FOR Z=1TO LEN(W$(X)):IF W1$(X,Z)="i"AND MID$(W$(X),Z,1)="*"GOTO1860
1850 NEXT Z:PRINT"THE LETTER . . . ";:GOTO2210
1860 PRINT"A BLANK.":Z$="*":X9=4:GOSUB5030:GOTO2930
1870 IF O=1THEN O=0:GOTO1910
1880 IF O=2THEN O=0:GOTO1920
1890 J=INT(7*RND(1)+1)
1900 ON J GOTO1910,1920,1930,1940,1950,1960,2000,2010
1910 J=INT(7*RND(1)):A$=A$(J):GOTO2080
1920 J=INT(3*RND(1)):A$=A1$(J):GOTO2080
1930 J=INT(3*RND(1)):A$=A2$(J):GOTO2080
1940 J=INT(3*RND(1)):A$=A3$(J):GOTO2080
1950 J=INT(5*RND(1)):A$=A4$(J):GOTO2080
1960 J=INT(2*RND(1)):A$=A5$(J):IF A$="K"GOTO2080
1970 IF LEN(W$(X))<4GOTO1890
1980 FOR Z=2TO LEN(W$(X))-2:IF W1$(X,Z)="U"AND W1$(X,Z-1)="i"GOTO2080
1990 NEXT Z:GOTO1890
2000 J=INT(3*RND(1)):A$=A6$(J):GOTO2080
2010 J$(X)="ETAONISRLDPFMBGETAONISRLDHCUPFMWYBGVKQXJZ"
2020 FOR Z=1TO 41:FOR Y=1TO LEN(K$(X))
2030 IF MID$(J$(X),Z,1)=MID$(K$(X),Y,1)GOTO2050
2040 J$=J$+MID$(J$(X),Z,1)
2050 NEXT Y:NEXT Z:J$(X)=J$:J$=""
2060 GOTO1870
2070 PRINTA$;".":Z$=A$:GOTO2930
2080 FOR Z=1TO 41:IF T<N THEN A$=Z$:GOTO2110
2090 FOR Y=1TO LEN(K$(X)):IF A$=MID$(K$(X),Y,1)GOTO1870
2100 NEXT Y:IF J$(X)=""GOTO2010
2110 IF Z=41AND A$=RIGHT$(J$(X),1)THEN J$(X)=LEFT$(J$(X),40):GOTO2170
2120 IF Z=1AND A$=LEFT$(J$(X),1)THEN J$(X)=MID$(J$(X),2):GOTO2170
2130 IF A$=MID$(J$(X),Z,1)GOTO2160
2140 NEXT Z:IF P1=1GOTO2170
2150 IF T=N THEN J=J+1:GOTO1900
2160 J$(X)=LEFT$(J$(X),Z-1)+MID$(J$(X),Z+1)
2170 IF P1=1THEN P1=0:RETURN 
2180 IF T1=1THEN T1=0:RETURN 
2190 IF T=N GOTO2070
2200 RETURN 
2210 J=0:FOR Z=3TO LEN(W$(X)):A$=W1$(X,Z)
2220 IF A$="A"OR A$="E"OR A$="I"OR A$="O"THEN J=1
2230 IF J=1AND W1$(X,Z-1)="U"AND W1$(X,Z-2)="i"THEN A$="Q":GOTO2510
2240 NEXT Z:J=0:FOR Z=1TO LEN(W$(X)):A$=W1$(X,Z)
2250 IF A$="S"OR A$="T"OR A$="C"OR A$="G"OR A$="P"THEN J=1
2260 IF J=1AND W1$(X,Z+1)="i"THEN A$="H":GOTO2510
2270 IF A$="O"AND W1$(X,Z+1)="i"THEN A$="N":GOTO2510
2280 NEXT Z:FOR Z=2TO LEN(W$(X))-2:A$=W1$(X,Z)+W1$(X,Z+1)+W1$(X,Z+2)
2290 IF A$="iiG"THEN A$="I":GOTO2510
2300 IF A$="IiG"THEN A$="N":GOTO2510
2310 IF A$="INi"THEN A$="G":GOTO2510
2320 IF A$="UDE"THEN A$="T":GOTO2510
2330 IF A$="iDE"AND W1$(X,Z-1)="T"THEN A$="U":GOTO2510
2340 IF A$="UiE"THEN A$="T":GOTO2510
2350 IF A$="ION"THEN A$="T":GOTO2510
2360 IF A$="iON"THEN A$="I":GOTO2510
2370 IF A$="iiN"OR A$="IiN"OR A$="Iii"THEN A$="O":GOTO2510
2380 IF A$="IOi"THEN A$="N":GOTO2510
2390 IF A$="iTE"THEN A$="A":GOTO2510
2400 IF A$="AiE"THEN A$="L":GOTO2510
2410 IF A$="iGH"OR A$="OiG"OR A$="iiG"THEN A$="U":GOTO2510
2420 IF A$="UGH"AND W1$(X,Z-1)="i"THEN A$="O":GOTO2510
2430 IF A$="UGi"OR A$="iGi"THEN A$="H":GOTO2510
2440 NEXT Z
2450 IF X=0 THEN 2470
2460 IF W1$(X,LEN(W$(X)))="Y"AND W1$(X,(LEN(W$(X))-1))="i"THEN A$="L":GOTO2510
2470 IF LEN(W$(X))<4GOTO2510
2480 FOR Z=4TO LEN(W$(X))
2490 IF (W1$(X,Z)="K"OR W1$(X,Z)="T")AND W1$(X,Z-1)="i"THEN A$="C":GOTO2510
2500 NEXT Z
2510 FOR Y=1TO LEN(K$(X)):IF A$=MID$(K$(X),Y,1)GOTO2530
2520 NEXT Y:J=0:GOTO2080
2530 GOTO1870
2540 I=X:K=0:PRINTY$;L$(1);
2550 PRINT"INTERRUPT RULE ACTIVATED! ":IF N=2THEN X=2:GOTO2610
2560 PRINT"WHICH PLAYER WILL GUESS? ";:Z$=INPUT$(1):PRINTZ$:X=VAL(Z$)
2570 A=X:PRINT"ARE YOU ";P$(X);"? ";:Z$=INPUT$(1):PRINTZ$:IF Z$="N"GOTO2560
2580 PRINT"OK, ";P$(X);", WHAT # WORD ARE YOU GUESSING? ";:Z$=INPUT$(1):PRINTZ$:X=VAL(Z$)
2590 IF X=A THEN PRINT"THAT'S YOUR WORD!";:X9=2:GOSUB5030:GOSUB3750:GOTO2580
2600 IF L(X)=1THEN PRINT"ALREADY EXPOSED!";:X9=2:GOSUB5030:GOSUB3750:GOTO2580
2610 IF L(X)=1THEN PRINT"MY WORD IS EXPOSED!":X9=2:GOSUB5030:GOSUB3750:RETURN 
2620 FOR Z=1TO LEN(W$(X)):IF W1$(X,Z)="i"THEN K=K+1
2630 NEXT Z:IF K<5THEN PRINT"ONLY";K;"LETTERS ARE UNEXPOSED."
2640 IF K<5THEN PRINT"THERE MUST BE AT LEAST 5 UNEXPOSED LETTERS.";
2650 IF K<5THEN X9=6:GOSUB5030:GOSUB3750:X=I:K=0:RETURN 
2660 PRINT"TYPE THE ENTIRE WORD, WITH BLANKS (IF ANY) IN PROPER POSITION"
2670 LINE INPUT "AND THEN PRESS THE 'RETURN KEY': ";Z$
2680 IF Z$<>W$(X)THEN PRINT"SORRY, ";P$(A);", YOU LOSE 50 POINTS!";
2690 IF Z$<>W$(X)THEN S(A)=S(A)-50:X=A:M=0:GOSUB3530:GOSUB3750:X=I:RETURN 
2700 GOSUB3750:PRINT
2710 PRINT"CONGRATULATIONS, ";P$(A);", YOU JUST WON 100 POINT\aS!";
2720 PRINT" HANG ON WHILE I COMPUTE..."
2730 S(A)=S(A)+100:FOR Z=1TO LEN(W$(X)):W1$(X,Z)=MID$(W$(X),Z,1):NEXT Z
2740 X=A:M=0:GOSUB3190:GOSUB3290:GOSUB3530:GOSUB3750:X=I:RETURN 
2750 FOR Z=1TO N:IF L(Z)=1THEN NEXT Z:GOTO2820
2760 S=0:FOR Y=1TO LEN(W$(Z)):IF W1$(Z,Y)="i"THEN S=S+1:S(Z)=S(Z)+P(Y):NEXT Y
2770 IF S>4THEN PRINTP$(Z);" HAS";S;"LETTERS UNEXPOSED AND GETS 100 POINTS "
2780 IF S>4THEN PRINT"IN ADDITION TO THE POINTS FOR EACH UNEXPOSED LETTER!"
2790 IF S>4THEN S(Z)=S(Z)+100:X9=6:GOSUB5030:GOSUB3750:GOSUB3450:RETURN 
2800 PRINTP$(Z);" GETS 50 POINTS IN ADDITION TO THE POINTS FOR EACH"
2810 PRINT"UNEXPOSED LETTER!":S(Z)=S(Z)+50:X9=6:GOSUB5030:GOSUB3750
2820 FOR Z=1TO N:FOR Y=1TO LEN(W$(Z)):W1$(Z,Y)=MID$(W$(Z),Y,1):NEXT Y:NEXT Z
2830 Q1=1:GOSUB3530:PRINT"CHECK THE WORDS FOR ACCEPTABILITY:"
2840 PRINT"NO HYPHENS, NO APOSTROPHES, NO BLANKS IN THE MIDDLE OF THE WORD,"
2850 PRINT"AND CORRECT SPELLING. ARE THERE ANY ERRORS? ";:Z$=INPUT$(1):PRINTZ$;
2860 GOSUB3750:IF Z$<>"Y"GOTO2920
2870 GOSUB3450:PRINT"WHAT IS THE # OF A PLAYER WITH AN ERROR? ";:Z$=INPUT$(1):PRINTZ$:X=VAL(Z$)
2880 PRINT"IS IT ";P$(X);:Z$=INPUT$(1):PRINTZ$:IF Z$<>"Y"GOTO2870
2890 PRINTP$(X);", YOU LOSE 100 POINTS!":S(X)=S(X)-100
2900 PRINT"ARE THERE ANY OTHERS WITH ERRORS";:Z$=INPUT$(1):PRINTZ$;:IF Z$<>"Y"GOTO2920
2910 GOTO2870
2920 PRINT"THEN HERE ARE THE FINAL RESULTS:":X9=2:GOSUB5030:GOSUB3450:RETURN 
2930 FOR Z=1TO LEN(W$(X)):IF Z$=MID$(W$(X),Z,1)AND W1$(X,Z)="i"THEN C=1
2940 IF C=0GOTO3070
2950 IF X=N THEN PRINT"GOOD GUESS. ";:A=X:X=Z:GOTO3020
2960 IF T=N THEN PRINT"YOU HAVE ";:A=X:GOTO2980
2970 PRINTP$(X);" HAS ";
2980 PRINT"AN UNEXPOSED ";Z$;". ";:A=X
2990 PRINT"WHAT IS ITS POSITION ";:GOSUB3690
3000 IF W1$(A,X)<>"i"THEN PRINT"THAT'S EXPOSED.";:GOTO2990
3010 IF MID$(W$(A),X,1)<>Z$THEN PRINT"WRONG. ";:GOTO2990
3020 IF A=N AND Z$="*"GOTO3130
3030 W1$(A,X)=Z$:Z=X:X=A:IF V>0THEN S(T)=S(T)+(V*P(Z)):M=1
3040 PRINT"THAT'S";:IF M=1THEN M=0:PRINTV*P(Z);:GOTO3060
3050 PRINTP(Z);:S(T)=S(T)+P(Z)
3060 PRINT"POINTS.":G=1:GOTO3110
3070 NEXT Z:IF Z$="*"THEN PRINT"NO BLANK. YOU LOSE 50 POINTS!":S(T)=S(T)-50
3080 X9=4:GOSUB 5030
3090 IF Z$="*"GOTO3110
3100 PRINT"NO POINTS.":X9=4:GOSUB5030:T1=1:IF Z$<>"*"THEN K$(X)=K$(X)+Z$
3110 V=0:IF X=N OR T=N THEN T1=0:RETURN 
3120 GOTO2080
3130 FOR Z=0TO LEN(W$(N))
3140 IF MID$(W$(N),Z+1,1)="*"AND W1$(N,Z+1)="i"GOTO3170
3150 IF MID$(W$(N),LEN(W$(N))-Z,1)="*"AND W1$(N,LEN(W$(N))-Z)="i"GOTO3180
3160 NEXT Z
3170 X=Z+1:GOTO3030
3180 X=LEN(W$(N))-Z:GOTO3030
3190 U=0:U1=0:FOR Z=1TO N:A$="":FOR Y=1TO LEN(W$(Z)):A$=A$+W1$(Z,Y)
3200 IF N=2AND Z=1GOTO3230
3210 IF W1$(Z,Y)="i"THEN U=U+1
3220 IF U>4THEN U1=1
3230 NEXT Y:U=0
3240 IF W$(Z)=A$THEN L(Z)=1
3250 NEXT Z:RETURN 
3260 IF L(X)=1AND G=1THEN PRINT"50 POINTS FOR COMPLETED WORD!":S(T)=S(T)+50
3270 IF L(X)=1AND G=1THEN X9=4:GOSUB5030
3280 G=0:RETURN 
3290 S=0:IF R=3THEN R=0:GOTO3390
3300 FOR Z=1TO N:IF L(Z)=1THEN S=S+1
3310 NEXT Z:IF S=N-1THEN S=0:GOTO3340
3320 IF S=N GOTO3400
3330 RETURN 
3340 IF T=Q THEN R=R+1
3350 IF Q=0THEN Q=T+1:IF T=N THEN Q=1
3360 IF R>0GOTO3380
3370 GOSUB3750:PRINT"STARTING WITH PLAYER";Q;"ONLY TWO MORE TURNS EACH.":X9=6:GOSUB5030
3380 RETURN 
3390 GOSUB3750:PRINTL$(1);"THAT WAS THE SECOND TRY FOR THE LAST WORD."
3400 PRINT"THAT'S THE END OF THE GAME.":X9=4:GOSUB5030:GOSUB3750:GOSUB2750
3410 GOSUB3750:PRINT"WANT TO PLAY AGAIN? ";:Z$=INPUT$(1):PRINTZ$;:IF Z$="N"THEN END 
3420 PRINTE$:Q=0:N=0:T=0:FOR Z=1 TO 4:S(Z)=0:L(Z)=0:P$(Z)="":W$(Z)="":NEXT
3430 FOR Z=1 TO 4:FOR II=1 TO 12:W1$(Z,II)="":NEXT II:NEXT Z
3440 D=11:O=1:U1=0:Q1=0:GOTO 120
3450 PRINTF$;X$;C0$;B$"Y  ";:FOR Z=32TO 70:PRINT"a";:NEXT Z:PRINT"s"
3460 PRINTB$"Y H";:FOR Z=72TO 111:PRINT"a";:NEXT Z:PRINT
3470 PRINTB$"Y( ";:FOR Z=32TO 70:PRINT"a";:NEXT Z:PRINT"b"
3480 PRINTB$"Y(H";:FOR Z=72TO 111:PRINT"a";:NEXT Z:PRINT
3490 PRINTB$"Y0 ";:FOR Z=32TO 70:PRINT"a";:NEXT Z:PRINT"u"
3500 PRINTB$"Y0H";:FOR Z=72TO 111:PRINT"a";:NEXT Z:PRINT
3510 FOR Z=33TO 39:PRINTB$"Y";CHR$(Z);"G`";:NEXT Z
3520 FOR Z=41TO 47:PRINTB$"Y";CHR$(Z);"G`";:NEXT Z:PRINT
3530 PRINTF$;C0$;:FOR Z=1TO N:PRINTP0$(Z);Z;P1$(Z);
3540 IF T=Z THEN PRINT"hhh";:GOTO3560
3550 PRINT"   ";
3560 FOR Y=1TO LEN(P$(Z)):PRINTMID$(P$(Z),Y,1);" ";:NEXT Y:PRINT
3570 PRINTP2$(Z):PRINTP3$(Z);"     ";P3$(Z);S(Z):PRINTP4$(Z);
3580 FOR Y=1TO LEN(W$(Z)):PRINTW1$(Z,Y);"  ";:NEXT Y:PRINT
3590 PRINTLEFT$(P5$(Z),4+LEN(W$(Z))*3)
3600 NEXT Z:PRINTG$;C1$:IF U1=0OR Q1=1THEN PRINTL$(1);:RETURN 
3610 GOSUB3750:PRINTX$:PRINTL$(8);SPC(12);K$;"ANYONE WANT TO GUESS A ";
3620 PRINT"WHOLE WORD? (Y/N) ";:GOSUB3640:GOSUB5000:IF Z$="Y"THEN GOSUB2540
3630 PRINTY$;L$(1);C1$:RETURN 
3640 DEF USR0=VARPTR(U0(0)):POKE 8219,0:POKE 8220,0
3650 Q2=USR0(0):IF Q2>64 THEN Q2=Q2 AND 95
3660 Z$=CHR$(Q2):IF Q2<>0 THEN PRINTZ$;:RETURN
3670 IF PEEK(8220)=20 THEN PRINT"TIME'S UP\a!":X9=3:GOSUB5030:RETURN
3680 GOTO 3650
3690 PRINT " (01,02,03,...12)? ";
3700 XX$=INPUT$(1):X=VAL(XX$):IF X>1 THEN PRINTTAB(20)"TRY AGAIN: ";:GOTO3690
3710 PRINTXX$;:XX$=INPUT$(1):Y=VAL(XX$)
3720 IF X=1 AND Y>2 THEN PRINTTAB(20)"TRY AGAIN: ";:GOTO3690
3730 PRINTXX$:IF X=1 THEN X=10+Y :ELSE X=Y
3740 RETURN
3750 FOR Y=1TO 7:PRINTL$(Y);K$;:IF Y<7THEN PRINT
3760 NEXT Y:PRINTL$(1);:RETURN 
3770 PRINTB$"Y+-";K$;"DO YOU WANT INSTRUCTIONS (Y/N)? ";:Z$=INPUT$(1):PRINTZ$;
3780 IF Z$="N"THEN PRINTB$"Y+-";K$;:RETURN 
3790 PRINTE$:N=4:Q=1:PRINTY$:OPEN "I",1,DK$+"WORD.INS"
3800 PRINT TAB(20)"*** INSTRUCTIONS ***":PRINT:PRINT:PRINTX$;L$(8)
3810 PRINT"PRESS THE SPACE BAR WHENEVER YOU SEE (S) TO CONTINUE INSTRUCTIONS.";
3820 PRINT B$"Y+;"K$"(S)";:GOSUB4160:PRINT E$:PRINT:PRINT
3830 FOR II=1 TO 11:LINE INPUT #1,XY$:PRINT XY$:NEXT:GOSUB4160
3840 PRINT E$:PRINT:PRINT:PRINT"HERE'S WHAT THE GAME STATUS DISPLAY";
3850 PRINT" LOOKS LIKE:  (S)";:GOSUB4160:PRINT E$
3860 P$(1)="GEORGE":P$(2)="MARYANNE":P$(3)="RALPH":P$(4)="COMPUTER"
3870 W$(1)="*WORD*":W$(2)="**TYPE***":W$(3)="SPELLING":W$(4)="GUESS*"
3880 FOR Z=1 TO 4:FOR Y=1 TO LEN(W$(Z)):W1$(Z,Y)="i":NEXT Y:NEXT Z:T=2
3890 GOSUB 3450:GOTO 3910
3900 GOSUB 3530:IF Q=3 THEN Q=4:GOTO 4020
3910 PRINT"DOES ANYONE WANT TO GUESS A WHOLE WORD? N":Q=Q+1
3920 IF Q=2 THEN PRINT"DRAW CARD READS: IF YOU HAVE A BLANK, EXPOSE IT."
3930 PRINT"----------"
3940 ON Q GOTO 3950,3950,3990,4020,4060
3950 LINE INPUT #1,XY$:PRINT XY$;:PRINT"("F$"hhh"G$"),"
3960 FOR II=1 TO 2:LINE INPUT #1,XY$:PRINT XY$:NEXT
3970 LINE INPUT #1,XY$:PRINT XY$;:GOSUB4160
3980 GOSUB3750:W1$(2,1)="*":GOTO3900
3990 FOR II=1 TO 3:LINE INPUT #1,XY$:PRINT XY$:NEXT
4000 LINE INPUT #1,XY$:PRINT XY$;:GOSUB4160
4010 GOSUB3750:W1$(1,2)="W":S(2)=4:GOTO3900
4020 FOR II=1 TO 6:LINE INPUT#1,XY$:PRINT XY$:NEXT
4030 LINE INPUT#1,XY$:PRINT XY$;:GOSUB4160
4040 GOSUB3750:W1$(1,1)="*":W1$(1,3)="O":W1$(1,4)="R":W1$(1,5)="D"
4050 W1$(1,6)="*":S(3)=122:GOTO3900
4060 FOR II=1 TO 2:LINE INPUT#1,XY$:PRINT XY$:NEXT
4070 LINE INPUT#1,XY$:PRINT XY$;:GOSUB4160:PRINT E$
4080 FOR II=1 TO 11:LINE INPUT#1,XY$:PRINT XY$:NEXT II:GOSUB4160:PRINT E$
4090 FOR II=1 TO 10:LINE INPUT#1,XY$:PRINT XY$:NEXT II:GOSUB4160:PRINT E$
4100 FOR II=1 TO 3:LINE INPUT #1,XY$:PRINT XY$:NEXT II:GOSUB4160:PRINT E$
4110 FOR II=1 TO 5:LINE INPUT #1,XY$:PRINT XY$:NEXT II
4120 PRINT"LET'S START. (S)";:GOSUB4160:Q=0:N=0:T=0
4130 FOR Z=1 TO 4:S(Z)=0:NEXT Z:PRINT E$:PRINT:PRINT TAB(20)"GOOD LUCK!";
4140 X9=4:GOSUB5030:FOR Z=1 TO 4:P$(Z)="":W$(Z)="":NEXT Z:PRINT E$:GOSUB4930
4150 CLOSE #1:RETURN
4160 XY$=INPUT$(1):IF XY$<>" "THEN GOTO4160 :ELSE RETURN
4170 PRINTC0$:PRINT"SHUFFLE THE 48 DRAW CARDS: ";:Y=0
4180 PRINTCHR$(7);:FOR Z=1TO 24:PRINT")(";:FOR II=1 TO 10:NEXT II:NEXT Z
4190 FOR Z=0TO 47:PRINTCHR$(8);:NEXT Z:PRINTCHR$(7);:X9=1:GOSUB5030
4200 PRINTCHR$(7);:FOR Z=0TO 23:PRINT"()";:FOR II=1 TO 10:NEXT II:NEXT Z:PRINTCHR$(7);
4210 FOR Z=0TO 47:PRINTCHR$(8);:NEXT Z:FOR Z=0TO 47:PRINT" ";:NEXT Z:Y=Y+1
4220 IF Y=3THEN PRINT" ";CHR$(8);CHR$(8);
4230 IF Y<3THEN FOR Z=0TO 47:PRINTCHR$(8);:NEXT Z:GOTO4180
4240 FOR Z=0TO 73:PRINTCHR$(8);:NEXT Z:Y=0
4250 PRINTB$"K";C1$:PRINTL$(1);:RETURN 
4260 PRINTB$"E";B$"x5"
4270 FOR Z=34TO 40:PRINTB$"Y";CHR$(Z);"$W";:NEXT Z
4280 X=42:FOR Z=37TO 41:X=X-1:PRINTB$"Y";CHR$(X);CHR$(Z);"W";:NEXT Z
4290 X=37:FOR Z=42TO 45:X=X+1:PRINTB$"Y";CHR$(X);CHR$(Z);"W";:NEXT Z
4300 FOR Z=40TO 34STEP -1:PRINTB$"Y";CHR$(Z);".W";:NEXT Z
4310 FOR Z=35TO 40:PRINTB$"Y";CHR$(Z);"8O";:NEXT Z
4320 FOR Z=57TO 65:PRINTB$"Y)";CHR$(Z);"O";:NEXT Z
4330 FOR Z=40TO 35STEP -1:PRINTB$"Y";CHR$(Z);"BO";:NEXT Z
4340 FOR Z=65TO 57STEP -1:PRINTB$"Y";CHR$(34);CHR$(Z);"O";:NEXT Z
4350 FOR Z=41TO 34STEP -1:PRINTB$"Y";CHR$(Z);"LR";:NEXT Z
4360 FOR Z=77TO 85:PRINTB$"Y";CHR$(34);CHR$(Z);"R";:NEXT Z
4370 FOR Z=35TO 36:PRINTB$"Y";CHR$(Z);"VR";:NEXT Z
4380 FOR Z=85TO 77STEP -1:PRINTB$"Y%";CHR$(Z);"R";:NEXT Z:PRINT
4390 X=82:FOR Z=38TO 41:X=X+1:PRINTB$"Y";CHR$(Z);CHR$(X);"R";:NEXT Z
4400 FOR Z=34TO 41:PRINTB$"Y";CHR$(Z);CHR$(96);"D";:NEXT Z
4410 FOR Z=97TO 103:PRINTB$"Y)";CHR$(Z);"D";:NEXT Z:PRINT
4420 X=41:FOR Z=104TO 106:X=X-1:PRINTB$"Y";CHR$(X);CHR$(Z);"D";:NEXT Z
4430 X=38:FOR Z=106TO 104STEP -1:X=X-1
4440 PRINTB$"Y";CHR$(X);CHR$(Z);"D";:NEXT Z:PRINT
4450 FOR Z=103TO 97STEP -1:PRINTB$"Y";CHR$(34);CHR$(Z);"D";:NEXT Z
4460 FOR Z=41TO 36STEP -1:PRINTB$"Y.";CHR$(Z);"S";:NEXT Z
4470 FOR Z=47TO 49:PRINTB$"Y";CHR$(Z);"#S";:NEXT Z
4480 FOR Z=36TO 40:PRINTB$"Y2";CHR$(Z);"S";:NEXT Z
4490 PRINTB$"Y3)S";B$"Y4)S"
4500 FOR Z=40TO 35STEP -1:PRINTB$"Y5";CHR$(Z);"S";:NEXT Z:PRINT
4510 FOR Z=46TO 53:PRINTB$"Y";CHR$(Z);"0E";:NEXT Z
4520 FOR Z=49TO 54:PRINTB$"Y.";CHR$(Z);"E";:NEXT Z
4530 FOR Z=49TO 53:PRINTB$"Y2";CHR$(Z);"E";:NEXT Z
4540 FOR Z=49TO 54:PRINTB$"Y5";CHR$(Z);"E";:NEXT Z:PRINT
4550 FOR Z=53TO 47STEP -1:PRINTB$"Y";CHR$(Z);"=A";:NEXT Z
4560 FOR Z=62TO 66:PRINTB$"Y.";CHR$(Z);"A";:NEXT Z
4570 FOR Z=47TO 53:PRINTB$"Y";CHR$(Z);"CA":NEXT Z
4580 FOR Z=62TO 66:PRINTB$"Y2";CHR$(Z);"A":NEXT Z
4590 FOR Z=53TO 46STEP -1:PRINTB$"Y";CHR$(Z);"JR":NEXT Z
4600 FOR Z=75TO 79:PRINTB$"Y.";CHR$(Z);"R":NEXT Z
4610 PRINTB$"Y/PR";B$"Y0PR";B$"Y1PR"
4620 FOR Z=79TO 75STEP -1:PRINTB$"Y2";CHR$(Z);"R":NEXT Z
4630 PRINTB$"Y3NR";B$"Y4OR";B$"Y5PR"
4640 FOR Z=93TO 88STEP -1:PRINTB$"Y.";CHR$(Z);"R":NEXT Z
4650 FOR Z=47TO 52:PRINTB$"Y";CHR$(Z);"WR":NEXT Z
4660 FOR Z=88TO 93:PRINTB$"Y5";CHR$(Z);"R":NEXT Z
4670 FOR Z=46TO 53:PRINTB$"Y";CHR$(Z);"dH":NEXT Z
4680 FOR Z=46TO 53:PRINTB$"Y";CHR$(Z);"iH":NEXT Z
4690 FOR Z=101TO 105:PRINTB$"Y2";CHR$(Z);"H":NEXT Z
4700 PRINTB$"Y+$";:FOR Z=36TO 108STEP 3
4710 PRINT"-->";CHR$(8);CHR$(8);CHR$(8);:FOR II=1 TO 50:NEXT II:PRINT"   ";:NEXT Z
4720 PRINTB$"E":PRINTB$"Y+ ";
4730 FOR Z=1TO 14STEP 3:PRINT"-->";CHR$(8);CHR$(8);CHR$(8);"   ";:NEXT Z
4740 PRINT B$"E"B$"y5"B$"Y+=ENTER DATA DISK DRIVE: ";:LINE INPUT DK$
4750 IF RIGHT$(DK$,1)<>":"THEN DK$=DK$+":"
4760 PRINTB$"E"B$"Y+=(\aONE MOMENT, PLEASE...)";B$"G";
4770 E$=B$+"E":K$=B$+"K":C0$=B$+"x5":C1$=B$+"y5"
4780 F$=B$+"F":G$=B$+"G":P$=B$+"p":Q$=B$+"q"
4790 FOR Z=1TO 8:L$(Z)=B$+"Y"+CHR$(Z+48)+" ":NEXT Z:REM LINES 18-25
4800 X$=B$+"x1":Y$=B$+"y1":REM ENABLE/DISABLE 25TH LINE
4810 P0$(1)=B$+"Y! #":P0$(2)=B$+"Y!H#":P0$(3)=B$+"Y) #"
4820 P0$(4)=B$+"Y)H#":P1$(1)=B$+"Y!)":REM PLAYER #/NAME LOCATION
4830 P1$(2)=B$+"Y!R":P1$(3)=B$+"Y))":P1$(4)=B$+"Y)R"
4840 P2$(1)=B$+"Y#,SCORE =":P3$(1)=B$+"Y#3":REM SCORE LOCATIONS
4850 P2$(2)=B$+"Y#USCORE =":P3$(2)=B$+"Y#\"
4860 P2$(3)=B$+"Y+,SCORE =":P3$(3)=B$+"Y+3"
4870 P2$(4)=B$+"Y+USCORE =":P3$(4)=B$+"Y+\"
4880 P4$(1)=B$+"Y%"+CHR$(34):P4$(2)=B$+"Y%K":REM WORD LOCATIONS
4890 P4$(3)=B$+"Y-"+CHR$(34):P4$(4)=B$+"Y-K"
4900 P5$(1)=B$+"Y'"+CHR$(34):P5$(2)=B$+"Y'K":REM POINT LOCATIONS
4910 P5$(3)=B$+"Y/"+CHR$(34):P5$(4)=B$+"Y/K"
4920 FOR Z=1TO 4:P5$(Z)=P5$(Z)+"2  4  6  6  4  2  2  4  6  6  4  2":NEXT Z
4930 PRINTX$;L$(8)
4940 PRINTTAB(10)"DO NOT USE THE `RETURN' KEY UNLESS INSTRUCTED";
4950 PRINT" TO DO SO.":RETURN 
4960 REM
4970 REM
4980 DIM U0(3):U0(0)=&H36:U0(1)=&H1FF:U0(2)=&H77D8:U0(3)=&HC9
4990 DEF USR0=VARPTR(U0(0))
5000 IF USR0(0)<>0 THEN 5000 :ELSE RETURN
5010 REM
5020 REM
5030 POKE 8219,0:POKE 8220,0
5040 IF PEEK(8220)=X9 THEN RETURN :ELSE 5040
