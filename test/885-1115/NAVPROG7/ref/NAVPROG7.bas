10 :REMARK  NAVPROG7.BAS\tAlan Bose\t28-Dec-81
20 CLEAR 500:ON ERROR GOTO 3240:WIDTH 255
30 BL$=CHR$(7):E$=CHR$(27):ER$=E$+"E":PG$=E$+"p":QG$=E$+"q":G$=E$+"F":NG$=E$+"G":Y$=E$+"Y":L$=E$+"l":J$=E$+"j":K$=E$+"k":J1$=E$+"J":U=1.790493109375
40 FOR I=8383 TO 8391:D5$=D5$+CHR$(PEEK(I)):NEXT I
50 DEF FNC$(C1,C2)=Y$+CHR$(C1+31)+CHR$(C2+31)
60 DEF FNS5(X)=SIN(X/U):DEF FNS6(X)=INT(X*10+0.5)/10
70 DEF FNS7(X)=ATN(X/SQR(1-X*X))*U
80 DEF FNS8(X)=SIN(ABS(A/2)/U)*COS(X/U)/SIN(Q2/2)
90 PRINT ER$;E$+"x1";FNC$(1,34);"NAVPROGseven";FN C$(25,1);ER$;E$+"H"
100 OPEN "R",1,"SY1:AIRPORTS.RND"
110 PRINT FNC$(25,1);ER$;
120 PRINT FNC$(5,1);L$;"Aircraft identification?  <MENU>  N";J$;STRING$(7,95);K$;:LINE INPUT X$:PRINT J1$
130 IF X$="" THEN LOAD"MENU",R
140 GOSUB3680:IF X$="I" THEN CLOSE:LOAD"AIRCRAFT",R
150 F$="N"+X$
160 OPEN"I",2,"SY1:"+F$
170 LINE INPUT#2,AT$
180 INPUT#2,RU
190 INPUT#2,T9
200 INPUT#2,G9
210 INPUT#2,D9
220 INPUT#2,NA
230 INPUT#2,NS
240 INPUT#2,NF
250 INPUT#2,NB
260 INPUT#2,PH
270 CLOSE#2
280 RC=(NA/T9)*60:CF=(G9/T9)*60:CS=(D9/T9)*60
290 PRINT FNC$(5,1);J1$;;FNC$(25,40-((LEN(AT$)+LEN(F$))/2));F$;" ";AT$
300 OPEN"I",2,"SY1:FLIGHT.SEQ"
310 FOR J=1 TO 10:INPUT#2,ID$(J)
320 INPUT #2,PI(J)
330 N=J
340 NEXT J:CLOSE#2
350 DIM C(N),T(N),V$(N),V(N),Y(N),P$(N),P1(N),P2(N),Q(N),G(N),W(N),S(N),E(N),F(N),F1(N),J(N),K(N),I$(N),FR(N),K8(N),FC$(N),EL(N),AL%(N)
360 :REMARK data box
370 PRINT FNC$(2,1);J1$
380 PRINT G$;FNC$(3,6);"f";
390 FOR J=7 TO 74:PRINT"a";:NEXT J:PRINT"c"
400 PRINT FNC$(4+N,6);"e";
410 FOR J=7 TO 74:PRINT"a";:NEXT J:PRINT"d"
420 PRINT FNC$(3,12);"s";FNC$(3,15);"s";FNC$(3,23);"s";FNC$(3,44);"s";FNC$(3,52);"s";FNC$(3,61);"s";FNC$(3,69);"s"
430 PRINT FNC$(4+N,12);"u";FNC$(4+N,15);"u";FNC$(4+N,23);"u";FNC$(4+N,44);"u";FNC$(4+N,52);"u";FNC$(4+N,61);"u";FNC$(4+N,69);"u"
440 FOR I=1 TO N
450 PRINT FNC$(3+I,6);"`";FNC$(3+I,12);"`";FNC$(3+I,15);"`";FNC$(3+I,23);"`";FNC$(3+I,44);"`";FNC$(3+I,52);"`";FNC$(3+I,61);"`";FNC$(3+I,69);"`";FNC$(3+I,75);"`"
460 NEXT I
470 PRINT NG$
480 PRINT FNC$(2,7);"Ident Fac Freq           Name           Lat     Long     Var   Elev"
490 FOR I=1 TO N
500 REC=(PI(I)MOD5)+1:SS=PI(I) \ 5
510 GET #1,REC
520 RO=I+3:GOSUB550
530 NEXT I:ERASE ID$,PI
540 GOTO 790
550 :REMARK decode & display
560 FIELD #1,SS*50 AS DU$,5 AS ID$,2 AS FAC$,4 AS FR$,20 AS NM$,2 AS D1$,4 AS M1$,2 AS D$,4 AS M$,4 AS V$,1 AS V1$,2 AS EL$
570 F5=CVI(FR$):D6=[0xFF][AA](D1$):M6=CVI(M1$):D5=[0xFF][AA](D$):M5=CVI(M$):V5=CVI(V$):E5=[0xFF][AA](EL$)
580 IF ID$<>ID$(I) THEN PRINT BL$;FNC$(N+6,1);"Aborted.  Checkpoint ";ID$(I);" missing or moved":PRINT"Delete this route, re-enter checkpoint if missing, re-enter route.":PRINT:PRINT"Hit <RETURN> to continue...";:X$=INPUT$(1):GOTO3180
590 I$(I)=ID$:FC$(I)=FAC$:FR(I)=F5:P$(I)=NM$
600 M1=M6/60:P2(I)=D6+M1
610 M=M5/60:P1(I)=D5+M
620 V(I)=V5:V$(I)=V1$:EL(I)=E5
630 PRINT FNC$(RO,1);I
640 PRINT FNC$(RO,7);ID$;FN C$(RO,13);FAC$
650 PRINT FNC$(RO,16);
660 IF F5=0 THEN PRINT "       ":GOTO 710
670 IF F5>136 THEN PRINT USING"####";F5:GOTO 710
680 IF F5*10MOD1=F5*10/1 THEN PRINT USING"####.#";F5:GOTO 710
690 IF F5*100MOD1=F5*100/1 THEN PRINT USING"####.##";F5:GOTO 710
700 PRINT USING"###.###";F5
710 PRINT FNC$(RO,24);NM$
720 PRINT FNC$(RO,45);USING"##";D6
730 PRINT FNC$(RO,48);USING"##.#";M6
740 PRINT FNC$(RO,53);USING"###";D5
750 PRINT FNC$(RO,57);USING"##.#";M5
760 PRINT FNC$(RO,62);USING"###.#";V5
770 PRINT FNC$(RO,68);V1$;FN C$(RO,70);USING"#####";E5
780 RETURN
790 PRINT FNC$(N+6,1);J1$;;"Route of flight correct? (Y or N)  <Y>  ";:X$=INPUT$(1):PRINT X$:IF X$=CHR$(13) THEN X$="Y"
800 GOSUB3680:IF X$<>"N" AND X$<>"Y" THEN PRINT BL$:GOTO 790
810 IF X$="N" THEN KILL"SY1:FLIGHT.SEQ":LOAD"MENU",R
820 R1=0:CLOSE
830 PRINT FNC$(N+6,1);J1$;"Flight Service Station you'll close with?  ";J$;STRING$(17,95);K$;:LINE INPUT X$
840 IF X$<>"" THEN GOSUB 3680:FS$=X$
850 FOR I=1 TO N
860 PRINT FN C$(N+6,1);J1$;
870 IF I=1 OR I=N THEN PRINT"Enter SURFACE wind at ";:GOTO 890
880 PRINT"Enter winds ALOFT at ";
890 PRINT I$(I);" ";P$(I);"  (deg,kts)  ";:INPUT W(I),S(I):IF S(I)=0 THEN 970
900 IF W(I)>360 THEN PRINT BL$:PRINT"360 degrees maximum":GOTO 870
910 IF S(I)<50 THEN 950 :ELSE PRINT BL$:PRINT"Wind > 50 knots?  (Y or N)  ";
920 X$=INPUT$(1):IF X$=CHR$(13) THEN X$="Y":GOSUB3680
930 IF X$<>"Y" AND X$<>"N" THEN PRINT BL$;:GOTO 910
940 IF X$="N" THEN 860
950 IF W(I)<=180 THEN W(I)=W(I)+180:GOTO 970
960 W(I)=W(I)-180
970 NEXT I
980 PRINT FNC$(N+6,1);J1$;"Enter usable fuel on board  <";NB;">  ";J$;STRING$(3,95);" gal";K$;
990 LINE INPUT X$:UF=VAL(X$):IF X$="" THEN UF=NB
1000 IF UF=0 THEN PRINT BL$;:GOTO 980
1010 NB=UF:K8(0)=UF-RU:F=K8(0)
1020 PRINT FNC$(N+6,1);J1$;:INPUT"Enter ZULU departure time  (hhmm)   ";HR
1030 IF HR>2400 THEN PRINT BL$:PRINT"2400 hours maximum":GOTO 1020
1040 L0=INT(HR/100):L1=(HR/100-L0)*100/60
1050 IF L1>1 THEN PRINT BL$:PRINT"60 minutes/hour":GOTO 1020
1060 CD=0:CK=0:CT=0:MT=0:OC=0
1070 PRINT FNC$(N+6,1);J1$;"Calculating..."
1080 FOR I=1 TO N-1:PRINT E$+"x4";FNC$(I+4,2);
1090 :REMARK distance
1100 A=P1(I)-P1(I+1)
1110 B1=P2(I)-P2(I+1)
1120 P=COS(P2(I)/U)*COS(P2(I+1)/U)
1130 Q=P*COS(ABS(A)/U)+COS(ABS(B1)/U)-P:IF Q<=0 THEN PRINT BL$:GOTO 3210
1140 Q2=ATN(SQR(1-Q*Q)/Q):Q=Q2*U*60
1150 C(I)=FNS6(Q):IFC(I)>900 AND ABS(A)>30 THEN PRINT BL$:GOTO3220
1160 IF C(I)=0 THEN T(I)=0:Y(I)=0:E(I)=0:G(I)=0:R=0:GOTO 2010
1170 :REMARK true course
1180 S=FNS8((P2(I)+P2(I+1))/2):IF S>=1 THEN S=90-S:GOTO 1200
1190 S=FNS7(S)
1200 IF A>0 AND B1=0 THEN T=90:GOTO 1260
1210 IF A<0 AND B1=0 THEN T=270:GOTO 1260
1220 IF A>0 AND B1<0 THEN T=S:GOTO 1260
1230 IF A>=0 AND B1>0 THEN T=180-S:GOTO 1260
1240 IF A<0 AND B1>0 THEN T=180+S:GOTO 1260
1250 T=360-S
1260 T(I)=FNS6(T)
1270 :REMARK magnetic course
1280 V1=(V(I)+V(I+1))/2
1290 V2=FNS6(V1)
1300 Y(I)=T(I)+V2
1310 IF Y(I)<0 THEN Y(I)=360-Y(I)
1320 IF Y(I)>360 THEN Y(I)=Y(I)-360
1330 IF I>1 THEN 1560
1340 PRINT E$+"y4";FNC$(N+6,1);J1$;"Initial magnetic course is";Y(I);"degrees.":PRINT "Enter cruise altitude  ";
1350 IF Y(I)<180 THEN PRINT"(Odd thousands)  ";::ELSE PRINT"(Even thousands)  ";
1360 PRINT "<";NA;">  ";J$;STRING$(5,95);K$;
1370 LINE INPUT X$:CA=VAL(X$):IF X$="" THEN CA=NA
1380 FOR J=1 TO N
1390 IF EL(J)+1000>CA THEN PRINT BL$;FNC$(N+6,1);J1$;"Low altitude...less than 1000 ft terrain clearance at ";P$(J):PRINT TAB(55);"Elev:";EL(J):PRINT"Hit <RETURN> to continue...";:X$=INPUT$(1):GOTO1340
1400 NEXT J:NA=CA
1410 PRINT FNC$(N+6,1);J1$;
1420 PRINT"Enter cruise TAS  <";NS;">  ";J$;STRING$(3,95);" kts";K$;
1430 LINE INPUT X$:TA=VAL(X$):IF X$="" THEN TA=NS
1440 IF TA>30 AND TA<250 THEN 1480
1450 PRINT BL$:PRINT"Is";TA;"knots the correct TAS?  (Y or N)  <Y>  ";
1460 X$=INPUT$(1):PRINT X$:IF X$=CHR$(13) THEN X$="Y"
1470 GOSUB3680:IF X$<>"Y" AND X$<>"N" THEN PRINT BL$:GOTO 1450 :ELSE IF X$="N" THEN 1420
1480 NS=TA:PRINT FNC$(N+6,1);J1$;"Enter cruise fuel consumption  <";NF;">  ";J$;STRING$(4,95);" gal";K$;
1490 LINE INPUT X$:F3=VAL(X$):IF X$="" THEN F3=NF
1500 IF F3>3 AND F3<50 THEN 1540
1510 PRINT BL$:PRINT"Is";F3;"gal/hr the correct fuel rate?  (Y or N)  <Y>  ";
1520 X$=INPUT$(1):PRINT X$:IF X$=CHR$(13) THEN X$="Y"
1530 GOSUB3680:IF X$<>"Y" AND X$<>"N" THEN PRINT BL$:GOTO 1510 :ELSE IF X$="N" THEN 1480
1540 NF=F3
1550 PRINT FNC$(N+6,1);J1$;"Calculating..."
1560 :REMARK cruise grndspd
1570 G0=I:A4=TAS:GOSUB 3280
1580 :REMARK climb?..climb grndspd
1590 IF I=1 THEN AT=CA-EL(1)
1600 AL%(I)=CA-AT
1610 IF AT<=0 THEN 1870
1620 G0=0:A4=CS:GOSUB 3280
1630 TT=AT/RC
1640 IF TT<C(I)/G(0) THEN 1750
1650 :REMARK climb entire leg
1660 CL=C(I)/G(0)
1670 CT=CT+CL
1680 AT=AT-(RC*CL)
1690 IF CA-AT<EL(I+1)+1000 THEN MT=1
1700 IF CA-AT<EL(I+1) THEN MT=2
1710 CD=CD+C(I)
1720 R=CT:FU=CF:GOSUB 3640
1730 K(I)=KU:CK=CK+KU
1740 G(I)=G(0):GOTO 1880
1750 :REMARK climb part of leg
1760 DC=TT*G(0)
1770 R=TT:FU=CF:GOSUB 3640
1780 K(I)=KU:CK=CK+KU
1790 CD=CD+DC:CT=CT+R
1800 G0=I:A4=TA:R=(C(I)-DC)/G(I)
1810 FU=F3:GOSUB 3640
1820 K(I)=K(I)+KU
1830 R=R+TT:G(I)=C(I)/R
1840 AT=0
1850 GOTO 1880
1860 :REMARK no climb
1870 FU=F3:R=C(I)/G(I):GOSUB 3640:K(I)=KU
1880 G(I)=FNS6(G(I))
1890 :REMARK magnetic heading
1900 IF T(I)+180>360 THEN 1930
1910 IF Q(I)>T(I) AND Q(I)<T(I)+180 THEN 1940
1920 GOTO 1960
1930 IF Q(I)<T(I) AND Q(I)>T(I)-180 THEN 1960
1940 E(I)=Y(I)-B:IF B<=Y(I) THEN 1980
1950 E(I)=360-E(I):GOTO 1980
1960 E(I)=Y(I)+B:IF E(I)<=360 THEN 1980
1970 E(I)=E(I)-360
1980 E(I)=FNS6(E(I))
1990 :REMARK time enroute
2000 R=C(I)/G(I)
2010 F(I)=INT(R)
2020 F1(I)=FNS6((R-F(I))*60)
2030 :REMARK ETA
2040 R1=R1+R
2050 O3=INT(R1):O4=(R1-O3)
2060 L3=INT(L1+O4)
2070 L2=INT((L1+O4-L3)*600+0.5)/10
2080 J(I)=L2:IF C(I)=0 THEN J(I)=0
2090 OC=OC+C(I)
2100 NEXT I:PRINT E$+"y4"
2110 :REMARK short flight...max alt?
2120 IF N=2 THEN I=1:G0=N:A4=TAS:GOSUB3280:GN=G(N) :ELSE GN=G(N-1)
2130 AL=CA-EL(N):DS=GN*1.6664999:DC=N-1:NR=DS/((GN/60)*1.4834278)
2140 IF OC>D9*3 THEN 2220
2150 OC=OC*1.4834278:PR=(RC/60)/((G(0)/60)*1.4834278)
2160 X=((OC*NR)+EL(N)-EL(1))/(PR+NR):Y=(PR*X)+EL(1)
2170 IF CA<=Y THEN 2220
2180 PRINT BL$;FNC$(N+6,1);"Unable to calculate flight profile to";CA;"feet."
2190 PRINT:PRINT"You can climb to";INT(Y);"feet before begining descent."
2200 PRINT"Select new cruise altitude and appropriate TAS & fuel consumption."
2210 PRINT:PRINT"Hit <RETURN> to continue...";:X$=INPUT$(1):GOTO 1060
2220 :REMARK descent profile
2230 DP=AL/1000*5:NC=0
2240 NC=NC+C(DC):IF DP<NC THEN 2280
2250 ND=NC*1.4834278:AL%(DC)=(NR*ND)+EL(N):IF AL%(DC)<EL(DC)+1000 THEN DW=1
2260 IF AL%(DC)<EL(DC) THEN DW=2
2270 DP=DP-C(DC):DC=DC-1:GOTO 2240
2280 :REMARK final ETA
2290 L0=INT(L0+R1+L1)
2300 IF L2=60 THEN L0=L0+1:L2=0
2310 IF L0<>2 OR L2<>0 THEN IF L0>23 THEN L0=L0-24:GOTO 2310
2320 O4=INT(O4*60+0.5)
2330 :REMARK print
2340 PRINT E$+"H";
2350 OPEN"O",1,"TT:"
2360 O=0
2370 IF H=1 THEN PRINT#1,F$;" ";AT$;TAB(31);"   NAVPROGseven";TAB(68);D5$:PRINT#1,STRING$(78,61)
2380 PRINT #1,TAB(20);"IDENT";TAB(34);"DIST";
2390 IF H=1 THEN PRINT#1,TAB(41);"MC/TC";::ELSE PRINT#1,TAB(42);"TC";
2400 PRINT#1,TAB(49);"MH";TAB(55);"GS";
2410 PRINT#1,TAB(61);"ETE";TAB(68);"ETA";TAB(73);"FUEL"
2420 PRINT#1,TAB(5);"CHECKPOINT";:IF H=1 THEN PRINT#1,TAB(20);"ID/fac";
2430 PRINT#1,TAB(27);"FREQ";
2440 IF H<>1 THEN PRINT#1,TAB(35);"nm";:GOTO2460
2450 PRINT#1,TAB(33);"rem/leg";
2460 PRINT#1,TAB(42);"deg";TAB(49);"deg";
2470 PRINT#1,TAB(55);"kts";TAB(61);"h:mm";TAB(68);":mm";
2480 IF H=1 THEN PRINT#1,TAB(72);"rem/leg"::ELSE PRINT#1,TAB(73);"gal"
2490 PRINT#1,STRING$(78,45)
2500 FOR I=1 TO N
2510 PRINT#1,TAB((20-LEN(P$(I)))/2);P$(I);
2520 PRINT#1,TAB(21);I$(I);
2530 IF FR(I)=0 THEN 2580
2540 IF FR(I)>136 THEN PRINT#1,USING"####";FR(I);:GOTO 2580
2550 IF FR(I)*10MOD1=FR(I)*10/1 THEN PRINT#1,USING"####.#";FR(I);:GOTO 2580
2560 IF FR(I)*100MOD1=FR(I)*100/1 THEN PRINT#1,USING"####.##";FR(I);:GOTO 2580
2570 PRINT#1,USING"###.###";FR(I);
2580 IF I=N THEN 2800
2590 IF H=1 THEN PRINT#1,TAB(33);USING "####.#";O9;:GOTO2610
2600 PRINT#1,TAB(33);USING "####.#";C(I);
2610 IF H=1 THEN PRINT#1,TAB(41);USING"###.#";Y(I);:GOTO2630
2620 PRINT#1,TAB(41);USING "###.#";T(I);
2630 PRINT#1,TAB(48);USING "###.#";E(I);
2640 PRINT#1,TAB(55);USING "###";G(I);
2650 PRINT#1,TAB(60);USING "##";F(I);
2660 PRINT#1,":";USING "##";F1(I);
2670 PRINT#1,TAB(68);":";USING "##";J(I);
2680 IF H=1 THEN PRINT#1,TAB(72);USING "###.#";K8(I-1);:GOTO2700
2690 PRINT#1,TAB(73);USING "##.#";K(I);
2700 IF K8(I)<=F3*0.5 THEN PRINT#1,"<<":GOTO2720
2710 IF K8(I)<F3*0.75 THEN PRINT#1,"<"::ELSE PRINT #1," "
2720 IF H<>1 THEN 2790
2730 PRINT #1,I;:IF I=1 THEN PRINT#1,TAB(5);"Elev:";EL(1);:GOTO2750
2740 IF AL%(I)<>AL%(I-1) THEN PRINT#1,TAB(5);"Alt:";AL%(I);
2750 PRINT#1,TAB(21);FC$(I);
2760 PRINT#1,TAB(33);USING"####.#";C(I);
2770 PRINT#1,TAB(41);USING"###.#";T(I);
2780 PRINT#1,TAB(73);USING "##.#";K(I)
2790 O=O+C(I):O1=O1+1:O9=O9-C(I)
2800 IF H=1 AND I=N THEN PRINT#1,TAB(34);"CLOSE FLIGHT PLAN WITH ";FS$;" FSS":PRINT#1,TAB(5);"Elev:";EL(N);TAB(21);FC$(N):PRINT #1,STRING$(78,61) :ELSE IF H=1 THEN PRINT#1, CHR$(10);
2810 IF I=N THEN IF H<>1 THEN PRINT#1,J1$:GOTO2830 :ELSE 2830
2820 NEXT I
2830 PRINT#1,CHR$(10);"Total dist =";O;"nm";TAB(30);"Time Off =";TAB(55);"Time On =":IF H=1 THEN PRINT#1,
2840 PRINT#1,"Total time =";O3;"hrs:";O4;"mins";TAB(30);"Depart at";HR;"GMT";
2850 PRINT#1,TAB(55);"Final ETA =";INT((L0*100)+L2);"GMT":IF H=1 THEN PRINT#1,
2860 F=UF-K8(N-1):F=FNS6(F)
2870 PRINT#1,"Avg. fuel usage =";F;"gal";TAB(30);"Fuel rate =";F3;"gal/hr";TAB(55);"Fuel on board =";UF;"gal":IF H=1 THEN PRINT#1,TAB(40);"(";FNS6(CF);"climb)"
2880 X8=INT(ABS(K8(I-1)/F3)):X9=INT((ABS(K8(I-1)/F3)-X8)*60)
2890 IF K8(I-1)<=0 THEN K8(I-1)=0:X8=0:X9=0
2900 PRINT#1,"Reserve =";K8(I-1);"gal =";X8;":";X9;TAB(30);"Cruise Alt =";CA;"ft";TAB(55);"TAS =";TA;"(";USING"###";CS;
2910 PRINT#1," climb)":IF H=1 THEN PRINT#1,TAB(55);"CAS =";INT(TA-(1.75*(CA/1000)));"(";INT(CS-(1.75*(CA/2000)));"climb)"
2920 PRINT#1,"Fuel to climb =";CK;"gal";TAB(30);"Dist to climb =";FNS6(CD);"nm";TAB(55);"Time to climb =";INT(CT*60);"min":IF H=1 THEN PRINT#1,
2930 PRINT#1,"Cost ";CHR$(64);USING"$$##.##";PH;
2940 PRINT#1,"/hr = ";USING"$$##.##";PH*R1
2950 IF MT=2 AND H=1 THEN PRINT#1,"WARNING! STANDARD CLIMB WILL NOT CLEAR TERRAIN DURING CLIMBOUT"
2960 IF MT=1 AND H=1 THEN PRINT#1,"CAUTION: MARGINAL TERRAIN CLEARANCE DURING CLIMBOUT"
2970 IF K8(I-1)=0 THEN PRINT#1,BL$;PG$;" INSUFFICIENT FUEL TO COMPLETE TRIP! ";QG$:GOTO 3090
2980 IF K8(I-1)<F3*0.5 THEN PRINT#1,BL$;PG$;" FUEL RESERVES LESS THAN VFR MINIMUMS! ";QG$:GOTO 3090
2990 IF H<>1 THEN 3090
3000 IF DW=2 AND H=1 THEN PRINT#1,"WARNING! NOMINAL DESCENT WILL NOT CLEAR TERRAIN!":GOTO3030
3010 PRINT#1,CHR$(10);"Nominal descent: ";INT(DS);"ft/min, ";INT(DP);"nm from ";I$(DC+1);" ";P$(DC+1)
3020 IF DW=1 AND H=1 THEN PRINT#1,"WARNING! MARGINAL TERRAIN CLEARANCE DURING DESCENT/APPROACH."
3030 PRINT#1,"Check charts for minimum terrain and obstruction ";"clearance altitudes."
3040 PRINT#1,:PRINT#1,"  --- WINDS ---"
3050 FOR J=1 TO N:PRINT#1,I$(J),
3060 IF W(J)>180 THEN W(J)=W(J)-180 :ELSE W(J)=W(J)+180
3070 IF S(J)>0 THEN PRINT#1,W(J);"degrees at";S(J);"knots." :ELSE PRINT #1,
3080 NEXT J
3090 CLOSE #1
3100 IF H=1 THEN H=0:O9=O:GOTO3160
3110 PRINT FNC$(N+13,1);J1$;"Hard copy printout? (Y or N)  <Y>  ";:X$=INPUT$(1):PRINT X$:IF X$=CHR$(13) THEN X$="Y"
3120 GOSUB3680:IF X$="Y" THEN H=1:HC=1
3130 IF X$="N" THEN 3160
3140 IF X$<>"Y" THEN PRINT BL$;:GOTO3110
3150 OPEN "O",1,"LP:":O9=O:GOTO2360
3160 PRINT FNC$(N+13,1);J1$;"Alternate winds aloft, cruise altitude? (Y or N)  <Y>  ";:X$=INPUT$(1):PRINT L$:IF X$=CHR$(13) THEN X$="Y"
3170 GOSUB3680:IF X$="Y" THEN O9=0:R1=0:PRINT FNC$(N+5,1);J1$:GOTO850 :ELSE IF X$<>"N" THEN PRINT BL$;:GOTO3160
3180 CLOSE:IF HC=1 THEN LOAD"SY1:RNAVREF",R :ELSE KILL"SY1:FLIGHT.SEQ":LOAD"MENU",R
3190 END
3200 :REMARK error trap
3210 PRINT FNC$(N+6,1);E$+"y4";BL$;"Distance ";P$(I);" to ";P$(I+1);" excessive...":PRINT "Hit <RETURN> to continue...":X$=INPUT$(1):GOTO 290
3220 PRINT FNC$(N+6,1);E$+"y4";BL$;"Distance ";P$(I);" to ";P$(I+1);" excessive."
3230 PRINT "Possible course errors due to rhumb line.":PRINT "Hit <RETURN> to continue...":X$=INPUT$(1):GOTO3180
3240 IF ERR=53 AND ERL=160 THEN PRINT"No data on file for ";F$:PRINT"Re-enter N-number or `I' to input data":RESUME 120
3250 IF ERR=53 AND ERL=300 THEN PRINT BL$;"No flight on file.":LOAD"MENU",R
3260 IF ERR=63 AND ERL=310 THEN N=J-1:J=11:RESUME 340
3270 ON ERROR GOTO 0
3280 :REMARK avg wind & grndspd
3290 IF S(I)=0 AND S(I+1)=0 THEN R2=0:Q(I)=T(I):GOTO 3480
3300 IF W(I)<W(I+1) THEN W3=W(I) :ELSE W3=W(I+1)
3310 IF W(I)>W(I+1) THEN W4=W(I) :ELSE W4=W(I+1)
3320 S3=S(I+1):S4=S(I)
3330 IF W3=W(I) THEN S3=S(I):S4=S(I+1)
3340 IF S3<>0 AND S4=0 THEN R2=S3/2:Q(I)=W3:GOTO 3480
3350 IF S3=0 AND S4<>0 THEN R2=S4/2:Q(I)=W4:GOTO 3480
3360 X=W4-W3:S1=S3*S3:S2=S4*S4
3370 IF X=0 THEN R2=(S3+S4)/2:Q(I)=W3:GOTO 3480
3380 IF X=180 THEN R2=0:Q(I)=T(I):GOTO 3480
3390 IF X>180 THEN X=360-X
3400 R=SQR(S1+S2-2*S3*S4*COS(X/U))
3410 Q=(S2+R*R-S1)/(2*S4*R)
3420 R2=SQR((S2+(R/2)*(R/2))-2*S4*(R/2)*Q)
3430 X1=(S2+R2*R2-(R/2)*(R/2))/(2*S4*R2)
3440 X1=ATN(SQR(1-X1*X1)/X1)*U
3450 IF X>180 THEN Q(I)=W4+X1:GOTO 3470
3460 Q(I)=W4-X1:GOTO 3480
3470 IF Q(I)>360 THEN Q(I)=Q(I)-360
3480 W=ABS(T(I)-Q(I))
3490 IF W<=180 THEN 3530
3500 IF Q(I)<T(I) THEN 3520
3510 W=360-Q(I)+T(I):GOTO 3530
3520 W=360-T(I)+Q(I)
3530 B=(R2*FNS5(W))/A4
3540 IF B>=1 THEN B=90:GOTO 3560
3550 B=FNS7(B)
3560 G=180-(W+B)
3570 J=FNS5(W)
3580 IF J=0 THEN R=A4+R2:GOTO 3620
3590 T=FNS5(G)
3600 IF T=0 THEN R=A4-R2:GOTO 3620
3610 R=(A4*T)/J
3620 G(G0)=R
3630 RETURN
3640 :REMARK fuel used
3650 KU=FNS6(R*FU)
3660 K8(I)=K8(I-1)-KU
3670 RETURN
3680 :REMARK map lc
3690 FOR L=1 TO LEN(X$):U$=MID$(X$,L,1)
3700 IF ASC(U$)>96 AND ASC(U$)<123 THEN MID$(X$,L,1)=CHR$(ASC(U$)-32)
3710 NEXT L:RETURN
