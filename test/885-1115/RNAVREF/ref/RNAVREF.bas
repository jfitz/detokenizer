10 :REMARK  RNAVREF.BAS\tNAVPROGseven RNAV Cross-Reference Program     30-Dec-81
20 :REMARK 
30 :REMARK  Developed by\tAlan Bose   (AOPA 642188)
40 :REMARK \t\t\tVice President, Taildragger Flyers
50 :REMARK \t\t\tRoss Field, Benton Harbor, MI
60 :REMARK 
70 CLEAR 1000:ON ERROR GOTO 920:WIDTH 255
80 BL$=CHR$(7):E$=CHR$(27):ER$=E$+"E":U=1.790493109375
90 PRINT"Checking RNAV references...";
100 DEF FNS5(X)=SIN(X/U):DEF FNS6(X)=INT(X*10+0.5)/10
110 DEF FNS7(X)=ATN(X/SQR(1-X*X))*U
120 DEF FNS8(X)=SIN(ABS(A/2)/U)*COS(X/U)/SIN(Q2/2)
130 :REMARK 
140 OPEN"I",1,"SY1:FLIGHT.SEQ"
150 FOR I=1 TO 10:LINE INPUT#1,P$(I)
160 INPUT#1,R(I)
170 N=I
180 NEXT I:CLOSE
190 :REMARK 
200 OPEN"I",1,"SY1:RNAVLIST.DAT"
210 INPUT#1,KY
220 DIM LI$(KY),R1$(KY),R1(KY),R2$(KY),R2(KY)
230 FOR J=1 TO KY:LINE INPUT#1,LI$(J)
240 LINE INPUT#1,R1$(J)
250 INPUT#1,R1(J)
260 LINE INPUT#1,R2$(J)
270 INPUT#1,R2(J)
280 NEXT J:CLOSE
290 :REMARK 
300 FOR I=1 TO N
310 FOR J=1 TO KY
320 IF P$(I)<>LI$(J) THEN 460 :ELSE CP=1
330 PI=R(I):K=0
340 GOSUB 510
350 PI=R1(J):K=1
360 GOSUB 510
370 IF R1$(J)="" THEN 390
380 L=1:M=0:GOSUB 620
390 IF R2$(J)="" THEN 460
400 PI=R2(J):K=2
410 GOSUB 510
420 IF R2$(J)="" THEN 460
430 L=2:M=0:GOSUB 620
440 IF R1$(J)="" OR R2$(J)="" THEN 460
450 L=1:M=2:GOSUB620
460 CP=0:NEXT J
470 NEXT I
480 CLOSE:KILL"SY1:FLIGHT.SEQ"
490 LOAD"MENU",R
500 :REMARK 
510 IF DE=0 THEN OPEN"R",1,"SY1:AIRPORTS.RND":DE=1
520 REC=(PIMOD5)+1:SS=PI \ 5
530 GET #1,REC
540 :REMARK decode
550 FIELD #1,SS*50 AS DU$,5 AS ID$,2 AS FAC$,4 AS FR$,20 AS NM$,2 AS D1$,4 AS M1$,2 AS D$,4 AS M$,4 AS V$,1 AS V1$,2 AS EL$
560 F5=CVI(FR$):D6=[0xFF][AA](D1$):M6=CVI(M1$):D5=[0xFF][AA](D$):M5=CVI(M$):V5=CVI(V$):E5=[0xFF][AA](EL$)
570 I$(K)=ID$:FC$(K)=FAC$:FR(K)=F5:NM$(K)=NM$
580 M1=M6/60:P2(K)=D6+M1
590 M=M5/60:P1(K)=D5+M
600 V(K)=V5:V$(K)=V1$:EL(K)=E5:GOTO610
610 RETURN
620 :REMARK distance
630 A=P1(L)-P1(M)
640 B1=P2(L)-P2(M)
650 P=COS(P2(L)/U)*COS(P2(M)/U)
660 Q=P*COS(ABS(A)/U)+COS(ABS(B1)/U)-P
670 Q2=ATN(SQR(1-Q*Q)/Q):Q=Q2*U*60
680 C=FNS6(Q)
690 IF C=0 THEN T=0:Y=0:R=0:GOTO 910
700 :REMARK true bearing
710 S=FNS8((P2(L)+P2(M))/2):IF S>=1 THEN S=90-S:GOTO 730
720 S=FNS7(S)
730 IF A>0 AND B1=0 THEN T=90:GOTO 790
740 IF A<0 AND B1=0 THEN T=270:GOTO 790
750 IF A>0 AND B1<0 THEN T=S:GOTO 790
760 IF A>=0 AND B1>0 THEN T=180-S:GOTO 790
770 IF A<0 AND B1>0 THEN T=180+S:GOTO 790
780 T=360-S
790 T=FNS6(T)
800 :REMARK magnetic bearing
810 V1=(V(L)+V(M))/2
820 V2=FNS6(V1)
830 Y=T+V2
840 IF Y<0 THEN Y=360-Y
850 IF Y>360 THEN Y=Y-360
860 :REMARK print
870 IF PT=0 THEN OPEN"O",2,"LP:":PT=1
880 IF CP=1 THEN PRINT#2,:PRINT#2,"RNAV bearings for ";I$(0);" ";NM$(0):CP=0
890 IF M=0 THEN PRINT#2,"NAV";L;": ";I$(L);FR(L);C;"nm ";Y;"deg Mag (";T;"True )"
900 IF M=2 THEN PRINT#2,"NAV 1 to NAV 2 : ";C;"nm ";Y;"deg Mag (";T;"True )"
910 RETURN
920 :REMARK error trap
930 IF ERL=140 AND ERR=53 THEN RESUME 480
940 IF ERL=150 AND ERR=63 THEN J=11:RESUME 180
950 IF ERL=200 AND ERR=53 THEN RESUME 480
960 IF ERL=480 AND ERR=53 THEN RESUME NEXT
970 ON ERROR GOTO 0
