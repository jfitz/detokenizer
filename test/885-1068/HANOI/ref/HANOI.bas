10 :REM \t\tHANOI.BAS
20 :REM \t\tTOWER OF HANOI
30 :REM \t\tWRITTEN 02/24/80 BY WILLIAM C. ZURNEY
40 CLEAR 2000:E$=CHR$(27):E1$=E$+"E":F$=E$+"F":G$=E$+"G":P$=E$+"p":Q$=E$+"q"
50 WIDTH 255:Y$=E$+"Y":X5$=E$+"x5":Y5$=E$+"y5":K$=E$+"K":H$=E$+"H"
60 DEFINT A-Z
70 ON ERROR GOTO 970
80 P0=12:DIM H(P0),F(P0),T(P0),U(P0),P6(2),P7(P0,2)
90 P6(0)=12:P6(1)=39:P6(2)=66
100 PRINT E1$;:INPUT "ENTER TOTAL NUMBER OF DISKS (1-12)";D
110 IF (D>=1) AND (D<=P0) THEN 130
120 PRINT "\aINVALID TOTAL NUMBER OF DISKS -- REENTER":GOTO 100
130 PRINT "Would you like the computer to play? <N> ";:D1$=INPUT$(1)
140 IF D1$="Y" OR D1$="y" THEN D1$="COM"
150 GOSUB 810
160 M1=2^D-1
170 PRINT H$Y5$;
180 IF D1$="COM" THEN 200
190 PRINT "FROM PEG? ";:D1$=INPUT$(1)
200 PRINT E$"b";
210 IF D1$="QUIT" OR D1$="Q" OR D1$="q" THEN 410
220 IF (Z=0) AND (D1$="COM") THEN 420
230 Z=1
240 IF (D1$<"1") OR (D1$>"3") THEN PRINT "\a";:GOTO 170
250 D1=VAL(D1$)-1
260 IF P7(0,D1)=0 THEN PRINT "\a";:GOTO 170
270 PRINT H$;
280 PRINT "TO PEG? ";:D2$=INPUT$(1)
290 PRINT E$"b";
300 IF (D2$<"1") OR (D2$>"3") THEN PRINT "\a";:GOTO 270
310 D2=VAL(D2$)-1
320 IF P7(0,D2)=0 THEN 340
330 IF P7(P7(0,D1),D1)>P7(P7(0,D2),D2) THEN PRINT "\a";:GOTO 170
340 PRINT X5$;
350 GOSUB 560
360 IF (P7(0,0)<>0) OR (P7(0,1)<>0) THEN 170
370 PRINT H$Y5$;
380 PRINT "CONGRATULATIONS -- IT TOOK YOU ";M;" MOVES"
390 IF M<>M1 THEN PRINT "MINIMUM NUMBER OF MOVES WOULD HAVE BEEN";M1
400 PRINT "PRESS ANY KEY WHEN FINISHED LOOKING. ";:X$=INPUT$(1)
410 GOSUB 950:END
420 :REM \t\tAUTO
430 PRINT X5$;
440 P=0:H(0)=D:F(0)=0:T(0)=2:U(0)=1
450 GOSUB 470
460 GOTO 370
470 :REM \t\tMOVE TOWER SUBROUTINE
480 IF H(P)<=0 THEN 540
490 P1=P+1:H(P1)=H(P)-1:F(P1)=F(P):T(P1)=U(P):U(P1)=T(P)
500 P=P1:GOSUB 470
510 D1=F(P):D2=T(P):GOSUB 560
520 P1=P+1:H(P1)=H(P)-1:F(P1)=U(P):T(P1)=T(P):U(P1)=F(P)
530 P=P1:GOSUB 470
540 P=P-1
550 RETURN
560 :REM \t\tMOVE DISK
570 M=M+1
580 X1=P7(0,D1):X2=P6(D1):X3=P7(X1,D1):P7(0,D1)=X1-1
590 FOR I=X1 TO D+3
600 X1=I:GOSUB 890
610 X1=I+1:GOSUB 930
620 NEXT I
630 IF (D2-D1)=0 THEN 750
640 PRINT Y$CHR$(55-D-4)" ";
650 IF (D2-D1)<0 THEN 720
660 PRINT E$CHR$(64);
670 FOR I=1 TO P6(D2)-P6(D1)
680 PRINT " ";:FOR I0=1 TO 5:NEXT I0
690 NEXT I
700 PRINT E$"O";
710 GOTO 750
720 FOR I=1 TO P6(D1)-P6(D2)
730 PRINT E$"N";:FOR I0=1 TO 5:NEXT I0
740 NEXT I
750 X1=P7(0,D2)+1:P7(0,D2)=X1:P7(X1,D2)=X3:X2=P6(D2)
760 FOR I=D+3 TO X1 STEP -1
770 X1=I+1:GOSUB 890
780 X1=I:GOSUB 930
790 NEXT I
800 RETURN
810 :REM \t\tINIT TOWERS
820 PRINT E1$F$X5$;
830 X3=0
840 FOR J=0 TO 2:X2=P6(J):FOR X1=1 TO D+1:GOSUB 890:NEXT X1:NEXT J
850 PRINT Y$"7 "STRING$(79,ASC("i"));
860 FOR I=0 TO 2:PRINT Y$"7"CHR$(32+P6(I))CHR$(ASC("1")+I);:NEXT I
870 X2=P6(0):X3=D+1:P7(0,0)=D
880 FOR X1=1 TO D:X3=X3-1:P7(X1,0)=X3:GOSUB 930:NEXT X1:PRINT H$;:RETURN
890 :REM \t\tERASE DISK
900 PRINT Y$CHR$(55-X1)CHR$(32+X2-X3)STRING$(2*X3+1,32);
910 IF X1<=D+1 THEN PRINT Y$CHR$(55-X1)CHR$(32+X2)"`";
920 RETURN
930 :REM \t\tDRAW DISK
940 PRINT Y$CHR$(55-X1)CHR$(32+X2-X3);P$STRING$(X3*2+1,122)Q$;:RETURN
950 :REM \t\tRESET TERMINAL
960 PRINT E$"z";:FOR 1 TO 100:NEXT J:RETURN
970 :REM \t\tERROR HANDLER
980 GOSUB 950
990 ON ERROR GOTO 0
