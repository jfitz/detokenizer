10 :REM \tSLOTS.BAS    BY PERRY S. PHIPPS \n\t\t\t125 EAST ARVADA STREET \n\t\t\tCOLORADO SPRINGS, CO  80906
20 CLEAR 2000:E$=CHR$(27):E1$=E$+"E":F$=E$+"F":G$=E$+"G":P$=E$+"p":Q$=E$+"q"
30 Y$=E$+"Y":X5$=E$+"x5":Y5$=E$+"y5":K$=E$+"K":A1$=CHR$(64)
40 PRINT E1$X5$"THIS IS A SIMULATED GAME OF SLOT MACHINE USING A COMPUTER TO "
50 PRINT "SPIN THE WHEELS. YOU WILL BE ABLE TO BET FROM $1.00 TO $5.00 "
60 PRINT "PER 'PULL'. BEFORE EACH SPIN YOU WILL BE ASKED TO BET. WHEN "
70 PRINT "READY TO PULL HANDLE, TYPE RETURN KEY FOR SPIN. "
80 PRINT "FOR $1.00 BET , WIN LINE IS CENTER OF WHEEL"
90 PRINT "FOR $2.00 BET , WIN LINE IS TOP    OF WHEEL,PLUS ALL ABOVE"
100 PRINT "FOR $3.00 BET , WIN LINE IS BOTTOM OF WHEEL,PLUS ALL ABOVE"
110 PRINT "FOR $4.00 BET , WIN LINE IS RIGHT DIAGONAL ,PLUS ALL ABOVE"
120 PRINT "FOR $5.00 BET , WIN LINE IS LEFT  DIAGONAL ,PLUS ALL ABOVE"
130 PRINT :PRINT F$TAB(20)"```"P$"GOOD LUCK"Q$"```"G$
140 X$="$####.##"
150 DIM A$(6),B$(6),C$(6),A(33)
160 FOR A=1 TO 3000:NEXT A
170 FOR A=1 TO 6
180 :REM \t\tREAD DATA FOR WHEEL
190 READ A$(A)
200 NEXT A
210 RESTORE 
220 FOR A=1 TO 6
230 READ B$(A)
240 NEXT A
250 RESTORE 
260 FOR A=1 TO 6
270 READ C$(A)
280 NEXT A
290 :REM \t\tCLEAR SCREEN AFTER INIT. INFORMATION
300 PRINT E1$
310 GOSUB 1740
320 PRINT Y$"4 "Y5$;
330 INPUT "WHAT IS YOUR BET ($1.00 TO $5.00) ";B1:PRINT X5$;
340 IF B1<1 OR B1>5 THEN GOTO 360
350 B1=INT(B1)
360 PRINT Y$"4 "K$
370 IF B1<1 OR B1>5 GOTO 330
380 GOSUB 1450
390 GOSUB 1430
400 A1=R
410 GOSUB 1430
420 A2=R
430 GOSUB 1430
440 A3=R
450 PRINT P$
460 GOSUB 1470
470 :REM \t\tREM THIS SUB FOR SPINNING WHEELS
480 FOR A=R1 TO 1 STEP -1
490 IF A<R1/2 THEN GOTO 560
500 A(11)=A(11)-1:IF A(11)=0 THEN A(11)=6
510 PRINT Y$"+3"A$(A(11))
520 A(12)=A(12)-1:IF A(12)=0 THEN A(12)=6
530 PRINT Y$",3"A$(A(12))
540 A(13)=A(13)-1:IF A(13)=0 THEN A(13)=6
550 PRINT Y$"-3"A$(A(13))
560 IF A<R1/1.5 THEN GOTO 630
570 A(21)=A(21)-1:IF A(21)=0 THEN A(21)=6
580 PRINT Y$"+"A1$B$(A(21))
590 A(22)=A(22)-1:IF A(22)=0 THEN A(22)=6
600 PRINT Y$","A1$B$(A(22))
610 A(23)=A(23)-1:IF A(23)=0 THEN A(23)=6
620 PRINT Y$"-"A1$B$(A(23))
630 A(31)=A(31)-1:IF A(31)=0 THEN A(31)=6
640 PRINT Y$"+M"C$(A(31))
650 A(32)=A(32)-1:IF A(32)=0 THEN A(32)=6
660 PRINT Y$",M"C$(A(32))
670 A(33)=A(33)-1:IF A(33)=0 THEN A(33)=6
680 PRINT Y$"-M"C$(A(33))
690 NEXT A
700 PRINT Q$
710 GOSUB 930
720 :REM \t\tCALCULATIONS FOR WINNINGS OR LOSSES
730 PRINT Y$" =";
740 IF W1>0 THEN PRINT " YOU ***WIN*** ";USING X$;W1
750 IF W1<1 THEN PRINT " SORRY YOU LOSE ";USING X$;B1
760 IF W1>0 THEN PRINT "TOTAL WINNINGS OF ";USING X$;W1
770 IF W1>0 THEN PRINT "MINUS BET OF ";USING X$;B1
780 PRINT "PREVIOUS TOTAL WAS ";USING X$;W
790 W=W+W1-B1
800 IF W>0 THEN PRINT "NEW TOTAL OF WINNINGS IS ";USING X$;W
810 IF W=0 THEN PRINT "YOU ARE NOW EVEN WITH THE BANK"
820 IF W<0 THEN PRINT "LOSSES SO FAR AMOUNT TO  ";USING X$;W
830 IF W<-100 THEN PRINT "****SORRY**** HOUSE LIMIT IS REACHED....PAY UP TURKEY"
840 IF W<-100 THEN GOTO 870
850 PRINT G$"Want to play again (Y or N) <Y> "Y5$;:Z$=INPUT$(1):PRINT F$X5$;
860 IF Z$="N" OR Z$="n" THEN PRINT E$"z":END
870 GOTO 300
880 :REM \t\tDRAWING OF OUTSIDE OF WHEELS
890 PRINT Y$")2";:FOR B=1 TO 3:PRINT "faaaaaac     ";:NEXT B:PRINT:
900 PRINT 
910 RETURN 
920 :REM \t\tSET UP FOR CHECKING FOR WINNING LINE
930 LET W1=0
940 LET W2=0
950 LET E=12:LET F=22:LET G=32
960 GOSUB 1640
970 IF W1>W2 THEN GOSUB 1160
980 IF B1<2 THEN RETURN
990 LET E=11:LET F=21:LET G=31
1000 GOSUB 1640
1010 IF W1>W2 THEN GOSUB 1220
1020 IF B1<3 THEN RETURN
1030 LET E=13:LET F=23:LET G=33
1040 GOSUB 1640
1050 IF W1>W2 THEN GOSUB 1280
1060 IF B1<4 THEN RETURN
1070 LET E=11:LET F=22:LET G=33
1080 GOSUB 1640
1090 IF W1>W2 THEN GOSUB 1340
1100 IF B1<5 THEN RETURN
1110 LET E=13:LET F=22:LET G=31
1120 GOSUB 1640
1130 IF W1>W2 THEN GOSUB 1380
1140 RETURN
1150 :REM \t\tPRINTOUT WINNING LINE ON SCREEN
1160 W2=W1
1170 PRINT Y$",-WINaa"
1180 PRINT Y$",:aaaaa"
1190 PRINT Y$",Gaaaaa"
1200 PRINT Y$",TaaWIN"
1210 RETURN
1220 W2=W1
1230 PRINT Y$"+-WINaa"
1240 PRINT Y$"+:aaaaa"
1250 PRINT Y$"+Gaaaaa"
1260 PRINT Y$"+TaaWIN"
1270 RETURN
1280 W2=W1
1290 PRINT Y$"--WINaa"
1300 PRINT Y$"-:aaaaa"
1310 PRINT Y$"-Gaaaaa"
1320 PRINT Y$"-TaaWIN"
1330 RETURN
1340 W2=W1
1350 PRINT Y$"*.WINy"
1360 PRINT Y$".UyWIN"
1370 RETURN
1380 W2=W1
1390 PRINT Y$"..WINx"
1400 PRINT Y$"*Ux";"WIN"
1410 RETURN
1420 :REM \t\tSET UP RANDOM SPIN OF WHEEL
1430 R=INT(RND(1)*10):IF R>6 OR R<1 THEN GOTO 1430
1440 RETURN
1450 R1=INT(RND(1)*100):IF R1>50  OR R1<2   GOTO 1450
1460 RETURN
1470 A(11)=A1
1480 A(21)=A2
1490 A(31)=A3
1500 IF A1=6 THEN A(12)=A1+1-6
1510 IF A2=6 THEN A(22)=A2+1-6
1520 IF A3=6 THEN A(32)=A3+1-6
1530 IF A1>4 THEN A(13)=A1+2-6
1540 IF A2>4 THEN A(23)=A2+2-6
1550 IF A3>4 THEN A(33)=A3+2-6
1560 IF A1<6 THEN A(12)=A1+1
1570 IF A2<6 THEN A(22)=A2+1
1580 IF A3<6 THEN A(32)=A3+1
1590 IF A1<5 THEN A(13)=A1+2
1600 IF A2<5 THEN A(23)=A2+2
1610 IF A3<5 THEN A(33)=A3+2
1620 RETURN
1630 :REM \t\tCHECKING FOR WIN LINE
1640 IF A(E)=1 AND A(F)=1 AND A(G)=1 THEN W1=W1+20
1650 IF A(E)=6 AND A(F)=6 AND A(G)=6 THEN W1=W1+10
1660 IF A(E)=3 AND A(F)=3 AND A(G)=3 THEN W1=W1+5
1670 IF A(E)=5 AND A(F)=5 AND A(G)=5 THEN W1=W1+1
1680 IF A(E)=3 AND A(F)=3 OR A(G)=3 THEN W1=W1+3
1690 IF A(E)=3 AND A(F)=3 OR A(G)=3 THEN RETURN
1700 IF A(F)=3 AND A(G)=3 THEN W1=W1+3
1710 IF A(F)=3 AND A(G)=3 THEN RETURN
1720 IF A(E)=3 OR A(F)=3 OR A(G)=3 THEN W1=W1+1
1730 RETURN
1740 PRINT F$
1750 GOSUB 880
1760 FOR A=1 TO 5
1770 PRINT Y$CHR$(41+A)"2";:FOR B=1 TO 3:PRINT "`      `     ";:NEXT B:PRINT:NEXT A
1780 PRINT Y$"/2";:FOR B=1 TO 3:PRINT "eaaaaaad     ";:NEXT B
1790 PRINT
1800 RETURN
1810 DATA BAR   ,BELL  ,CHERRY,APPLE ,LEMON ,$$$$$$
