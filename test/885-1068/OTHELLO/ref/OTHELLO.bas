10 :REM \t\tOTHELLO.BAS\tBy: Richard Musgrave \n\t\t\t\t    5101 N. Elmwood \n\t\t\t\t    Kansas City, MO  64119
20 CLEAR 2000:WIDTH 255
30 E$=CHR$(27):E1$=E$+"E":P$=E$+"p":Q$=E$+"q":F$=E$+"F":G$=E$+"G"
40 Y$=E$+"Y":X5$=E$+"x5":Y5$=E$+"y5":K$=E$+"K":L$=STRING$(6," ")+"`":
50 FOR C=7 TO 56 STEP 7:L1$=Y$+"9"+CHR$(C+32)+"`":L$=L1$+L$:NEXT C
60 M$="aaaaaab":M$="v"+M$+M$+M$+M$+M$+M$+M$+"aaaaaat"
70 Z=RND(-PEEK(8219)*PEEK(8219)):H1$=P$+"    "+Q$:H2$="iiii"
80 PRINT E1$E$"x4"X5$;
90 DIM A(200),A1(100):U=33:V=66:FOR I=0 TO 99:READ A(I):A1(I)=A(I):NEXT I
100 DATA 0,0,0,0,0,0,0,0,0,0,0,-99,-1,-15,-15,-15,-15,-1,-99,0
110 DATA 0,-1,-1,-5,-5,-5,-5,-1,-1,0,0,-15,-5,-12,-10,-10,-12,-5,-15,0
120 DATA 0,-15,-5,-10,1,2,-10,-5,-15,0,0,-15,-5,-10,2,1,-10,-5,-15,0
130 DATA 0,-15,-5,-12,-10,-10,-12,-5,-15,0,0,-1,-1,-5,-5,-5,-5,-1,-1,0
140 DATA 0,-99,-1,-15,-15,-15,-15,-1,-99,0,0,0,0,0,0,0,0,0,0,0
150 PRINT Y5$;:INPUT "SKILL LEVEL (1 TO 5)";Z2:ON Z2 GOTO 210,200,190,180,170
160 GOTO 150
170 A(81)=2
180 A(18)=2
190 A(88)=2
200 A(11)=2
210 INPUT "WHO STARTS (1=ME , 2=YOU)";Z
220 FOR I=9 TO 12:J=11*INT(I/12):A(91+I)=I-J:A(95+I)=J-I:NEXT I:GOSUB 640
230 GOSUB 810:IF Z=2GOTO 430
240 GOSUB 860:PRINT G$Y$"-\I'm thinking"F$:C=0:D=0:FOR F=U TO V:IF A(F)=0 THEN F=F+2
250 IF A(F)>0 GOTO 330
260 B=0:FOR K=100 TO 107:J=F:A=0
270 J=J+A(K):IF A(J)<1 GOTO 300
280 IF A(J)=1 THEN A=A-A(F):GOTO 270
290 B=B+A
300 NEXT K:IF B=0 GOTO 330
310 IF B>C THEN C=B:A(108)=F:D=1
320 IF B=C THEN A(108+D)=F:D=D+1
330 NEXT F:IF D>0 GOTO 360
340 PRINT Y$"-\I HAVE NO MOVES!":IF Q=1 GOTO 870
350 S5=1:GOTO 430
360 I=A(107+INT(D*RND(1)+1)):PRINT Y$"-\MY MOVE IS "I
370 A(I)=2:GOSUB 720:J=I:Q=0:FOR K=100 TO 107:A=0:I=J
380 I=I+A(K):IF A(I)<1 GOTO 420
390 IF A(I)=2 GOTO 410
400 A=A+1:GOTO 380
410 I=J:IF A>0 THEN Z=Z:FOR L=1 TO A:I=I+A(K):A(I)=2:GOSUB 720:NEXT L
420 NEXT K:GOSUB 810
430 PRINT Y$".\\aYOUR MOVE "Y5$;:INPUT M:PRINT X5$:M=INT(M):IF M=0 GOTO 620
440 IF M=1 THEN Z=Z:GOSUB 640:GOSUB 810:GOTO 430
450 IF M=2 THEN S1=0:S2=64:GOTO 870
460 IF M=3 GOTO 920
470 IF M=4 GOTO 1040
480 IF (M<11) OR (M>88) OR (A(M)>0) GOTO 610
490 S5=0:B=0:FOR K=100 TO 107:J=M:A=0
500 J=J+A(K):IF A(J)<1 GOTO 550
510 IF A(J)=2 THEN A=A+1:GOTO 500
520 I=M:IF A=0 GOTO 550
530 IF A(I)<0 THEN A(I)=1:GOSUB 720
540 FOR Z1=1 TO A:I=I+A(K):A(I)=1:B=B+1:GOSUB 720:NEXT Z1
550 NEXT K:IF B=0 GOTO 610
560 W=M-11:IF W<U THEN U=W
570 IF A(U)=0 THEN U=U+1:GOTO 570
580 W=M+11:IF W>V THEN V=W
590 IF A(V)=0 THEN V=V-1:GOTO 590
600 GOSUB 810:GOTO 240
610 PRINT Y$"/\\aILLEGAL MOVE":GOSUB 860:GOTO 430
620 IF S5=1 GOTO 870
630 Q=1:GOTO 240
640 PRINT E1$F$X5$;
650 PRINT Y$"9 faaa1aasaaa2aasaaa3aasaaa4aasaaa5aasaaa6aasaaa7aasaaa8aac"
660 FOR I=1 TO 8
670 PRINT "`"L$:PRINTY$"9 "RIGHT$(STR$(I),1);:IF I=8 THEN PRINTL$;:GOTO 690
680 PRINT Y$"9!"L$:PRINT Y$"9 "M$:NEXT I
690 PRINT E$"x1"Y$"8 eaaaaaauaaaaaauaaaaaauaaaaaauaaaaaauaaaaaauaaaaaauaaaaaad"
700 FOR I=11 TO 88:IF A(I)>0 THEN Z=Z:GOSUB 720
710 NEXT I:GOSUB 770:RETURN 
720 R=INT(I/10):C=I-R*10:R1=R*3+30:C1=C*7+27
730 PRINT Y$"  "
740 IF A(I)=2 THEN PRINT Y$CHR$(R1)CHR$(C1)H2$Y$CHR$(R1+1)CHR$(C1)H2$;:RETURN
750 IF A(I)<0 THEN PRINT Y$CHR$(R1)CHR$(C1)"    "Y$CHR$(R1+1)CHR$(C1)"    ";:RETURN
760 PRINT Y$CHR$(R1)CHR$(C1)H1$Y$CHR$(R1+1)CHR$(C1)H1$;:RETURN 
770 PRINT Y$" aOTHELLO"Y$CHR$(34)"\ME = ii   YOU = "P$"  "Q$
780 PRINT Y$"%\SKILL LEVEL"Z2;Y$"'\NO MOVE =\t\t0"
790 PRINT Y$"(\RESET BOARD =\t1"Y$")\RESIGN =\t\t2"
800 PRINT Y$"*\SET UP GAME =\t3"Y$"+\REVERSE =\t\t4":RETURN
810 S1=0:S2=0:FOR I=11 TO 88:IF A(I)=1 THEN S1=S1+1
820 IF A(I)=2 THEN S2=S2+1
830 NEXT I
840 PRINT Y$"#\"S2;Y$"#f"S1:IF (S1=0) OR (S2=0) OR (S1+S2=64) GOTO 870
850 RETURN 
860 FOR I1=1TO 10:PRINT Y$CHR$(I1+44)"\"K$:NEXT I1:RETURN
870 GOSUB 860:IF S1=S2 THEN PRINT G$Y$"0\Tied Game":GOTO 900
880 IF S1>S2 THEN PRINT G$Y$"0ayou win"Y$"1\goat droppings....":GOTO 900
890 FOR I=1TO 8:PRINT Y$CHR$(I+44)"\COMPUTER WINS !!!!":NEXT I
900 PRINT Y$"6\PLAY AGAIN(1=YES)"Y5$;:INPUT M:IF M=1 GOTO 20
910 PRINT E$"z":FOR I=1 TO 400:NEXT I:END
920 GOSUB 860:PRINT Y$"-\MY PIECE =2"
930 PRINT Y$".\YOUR PIECE =1"Y$"/\RETURN =0"Y$"0\BLANK =-1"
940 PRINT Y$"2\ENTER PIECE";:INPUT R:R=INT(R):IF R=0 GOSUB 810:GOSUB 860:GOTO430
950 IF R=-1 GOTO 970
960 IF (R<>1)AND (R<>2)GOTO 1030
970 PRINT Y$"3\ENTER POSITION";:INPUT N
980 N=INT(N):IF (N>=11)AND (N<=88) GOTO 1000
990 PRINT Y$"3\"K$:GOTO 970
1000 IF A(N)=0 GOTO 990
1010 IF R=-1 THEN R=A1(N)
1020 I=N:A(I)=R:GOSUB 720
1030 PRINT Y$"2\"K$Y$"3\"K$:GOTO 940
1040 FOR I=11TO 88:IF A(I)=1 THEN A(I)=2:GOSUB 720:GOTO 1060
1050 IF A(I)=2 THEN A(I)=1:GOSUB 720
1060 NEXT I:GOSUB 810:GOSUB 860:GOTO 430
